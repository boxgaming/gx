$If GXSTR = UNDEFINED Then
    ' Pseudo-Constants
    ' ------------------------------------------------------------------------
    Function GX_LF$: GX_LF = Chr$(10): End Function
    Function GX_CR$: GX_CR = Chr$(13): End Function
    Function GX_CRLF$: GX_CRLF = GX_CR + GX_LF: End Function

    ' String Utility Methods
    ' ------------------------------------------------------------------------
    Function GXSTR_Replace$ (s As String, searchString As String, newString As String)
        Dim ns As String
        Dim i As Integer

        Dim slen As Integer
        slen = Len(searchString)

        For i = 1 To Len(s) '- slen + 1
            If Mid$(s, i, slen) = searchString Then
                ns = ns + newString
                i = i + slen - 1
            Else
                ns = ns + Mid$(s, i, 1)
            End If
        Next i

        GXSTR_Replace = ns
    End Function

    Function GXSTR_Split (sourceString As String, delimiter As String, results() As String)
        ' Modified version of:
        ' https://www.qb64.org/forum/index.php?topic=1073.msg102711#msg102711
        Dim cstr As String, p As Long, curpos As Long, arrpos As Long, dpos As Long

        ' Make a copy of the source string
        cstr = sourceString

        ' Special case if the delimiter is space, remove all excess space
        If delimiter = " " Then
            cstr = RTrim$(LTrim$(cstr))
            p = InStr(cstr, "  ")
            While p > 0
                cstr = Mid$(cstr, 1, p - 1) + Mid$(cstr, p + 1)
                p = InStr(cstr, "  ")
            Wend
        End If
        curpos = 1
        arrpos = 0
        dpos = InStr(curpos, cstr, delimiter)
        Do Until dpos = 0
            arrpos = arrpos + 1
            ReDim _Preserve results(arrpos) As String
            results(arrpos) = Mid$(cstr, curpos, dpos - curpos)
            curpos = dpos + Len(delimiter)
            dpos = InStr(curpos, cstr, delimiter)
        Loop
        arrpos = arrpos + 1
        ReDim _Preserve results(arrpos) As String
        results(arrpos) = Mid$(cstr, curpos)

        GXSTR_Split = arrpos
    End Function

    Function GXSTR_LPad$ (s As String, padChar As String, swidth As Integer)
        Dim padding As String
        padding = String$(swidth - Len(s), padChar)
        GXSTR_LPad = padding + s
    End Function

    Function GXSTR_RPad$ (s As String, padChar As String, swidth As Integer)
        Dim padding As String
        padding = String$(swidth - Len(s), padChar)
        GXSTR_RPad = s + padding
    End Function

    $Let GXSTR = 1
$End If
