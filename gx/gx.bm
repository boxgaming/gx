$If GXBI = UNDEFINED Then
    Option _Explicit
    '$Include: './gx.bi'

    Sub GXOnGameEvent (e As GXEvent)
        Select Case e.event
        End Select
    End Sub
$End If
$If GXBM = UNDEFINED Then

    ' System Methods
    ' ----------------------------------------------------------------------------
    Sub GXSleep (stime)
        _Delay stime
    End Sub

    Function GXMouseX%
        GXMouseX = _MouseX / __gx_scene.scaleX
    End Function

    Function GXMouseY%
        GXMouseY = _MouseY / __gx_scene.scaleY
    End Function

    Sub GXHardwareAcceleration (hardwareAcceleration As Integer)
        __gx_hardware_acceleration = hardwareAcceleration
    End Sub

    Function GXHardwareAcceleration
        GXHardwareAcceleration = __gx_hardware_acceleration
    End Function


    ' Sound Methods
    ' ----------------------------------------------------------------------------
    Function GXSoundLoad (filename As String)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            GXSoundLoad = _SndOpen(filename)
        $End If
    End Function

    Sub GXSoundPlay (sid As Long)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            If Not GXSoundMuted Then _SndPlay sid
        $End If
    End Sub

    Sub GXSoundRepeat (sid As Long)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            If Not GXSoundMuted Then _SndLoop sid
        $End If
    End Sub

    Sub GXSoundVolume (sid As Long, v)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            _SndVol sid, v / 100
        $End If
    End Sub

    Sub GXSoundPause (sid As Long)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            _SndPause sid
        $End If
    End Sub

    Sub GXSoundStop (sid As Long)
        $If MACOSX Then
            ' Sound not currently supported for macosx
        $Else
            _SndStop sid
        $End If
    End Sub

    Sub GXSoundMuted (muted As Integer)
        __gx_sound_muted = muted
        'TODO: maintain a list of loaded sounds so they can all be muted
    End Sub

    Function GXSoundMuted
        GXSoundMuted = __gx_sound_muted
    End Function

    Sub GXEntityAnimate (eid As Integer, seq As Integer, a As Integer)
        __gx_entities(eid).animate = a
        __gx_entities(eid).spriteSeq = seq
        __gx_entities(eid).prevFrame = -1
    End Sub

    Sub GXEntityAnimateStop (eid As Integer)
        __gx_entities(eid).animate = 0
    End Sub

    Sub GXEntityAnimateMode (eid As Integer, mode As Integer)
        __gx_entities(eid).animateMode = mode
    End Sub

    Function GXEntityAnimateMode (eid As Integer)
        GXEntityAnimateMode = __gx_entities(eid).animateMode
    End Function

    Function GXScreenEntityCreate (imageFilename As String, ewidth As Integer, height As Integer, seqFrames As Integer)
        Dim eid As Integer
        eid = GXEntityCreate(imageFilename, ewidth, height, seqFrames)
        __gx_entities(eid).screen = 1
        GXScreenEntityCreate = eid
    End Function

    Function GXEntityCreate (imageFilename As String, ewidth As Integer, height As Integer, seqFrames As Integer)
        Dim newent As GXEntity
        newent.x = 0
        newent.y = 0
        newent.height = height
        newent.width = ewidth
        If _FileExists(imageFilename) Then
            newent.image = __GX_ImageLoad(imageFilename)
            'Print "newent.image: ", newent.image
        Else
            newent.image = __gx_img_blank
        End If
        newent.spriteFrame = 1
        newent.spriteSeq = 1
        newent.seqFrames = seqFrames
        __gx_entity_count = __gx_entity_count + 1
        ReDim _Preserve __gx_entities(__gx_entity_count) As GXEntity
        __gx_entities(__gx_entity_count) = newent
        GXEntityCreate = __gx_entity_count
    End Function

    Sub GXEntityCreate (imageFilename As String, ewidth As Integer, height As Integer, seqFrames As Integer, uid As String)
        Dim eid As Integer
        eid = GXEntityCreate(imageFilename, ewidth, height, seqFrames)
        __GX uid, eid, GXTYPE_ENTITY
    End Sub

    Sub GXEntityHide (eid As Integer)
        __gx_entities(eid).hidden = GX_TRUE '1
    End Sub

    Sub GXEntityShow (eid As Integer)
        __gx_entities(eid).hidden = GX_FALSE '0
    End Sub

    Sub __GX_EntityDraw (ent As GXEntity)
        If ent.hidden Then Exit Sub
        Dim x, y As Long
        If ent.screen = 1 Then
            x = ent.x
            y = ent.y
        Else
            x = ent.x - GXSceneX
            y = ent.y - GXSceneY
        End If
        GXSpriteDraw ent.image, x, y, ent.spriteSeq, ent.spriteFrame, ent.width, ent.height, __gx_scene.image
        If GXDebug Then __GX_DebugEntity ent, x, y
    End Sub

    Sub GXEntityMove (eid As Integer, x As Long, y As Long)
        __gx_entities(eid).x = __gx_entities(eid).x + x
        __gx_entities(eid).y = __gx_entities(eid).y + y
    End Sub


    Sub GXEntityPos (eid As Integer, x As Long, y As Long)
        __gx_entities(eid).x = x
        __gx_entities(eid).y = y
    End Sub

    Sub GXEntityVX (eid As Integer, vx As Double)
        __gx_entities(eid).vx = vx
    End Sub

    Function GXEntityVX (eid As Integer)
        GXEntityVX = __gx_entities(eid).vx
    End Function

    Sub GXEntityVY (eid As Integer, vy As Double)
        __gx_entities(eid).vy = vy
    End Sub

    Function GXEntityVY (eid As Integer)
        GXEntityVY = __gx_entities(eid).vy
    End Function


    Function GXEntityX (eid As Integer)
        GXEntityX = __gx_entities(eid).x
    End Function

    Function GXEntityY (eid As Integer)
        GXEntityY = __gx_entities(eid).y
    End Function

    Function GXEntityWidth (eid As Integer)
        GXEntityWidth = __gx_entities(eid).width
    End Function

    Function GXEntityHeight (eid As Integer)
        GXEntityHeight = __gx_entities(eid).height
    End Function

    Sub GXEntityFrameNext (eid As Integer)
        If __gx_entities(eid).animateMode = GXANIMATE_SINGLE Then
            If __gx_entities(eid).spriteFrame + 1 >= __gx_entities(eid).seqFrames Then
                If Not __gx_entities(eid).spriteFrame = __gx_entities(eid).prevFrame Then
                    ' Fire animation complete event
                    Dim e As GXEvent
                    e.event = GXEVENT_ANIMATE_COMPLETE
                    e.entity = eid
                    GXOnGameEvent e
                    __gx_entities(eid).prevFrame = __gx_entities(eid).spriteFrame
                End If
                Exit Sub
            End If
        End If

        __gx_entities(eid).prevFrame = __gx_entities(eid).spriteFrame
        __gx_entities(eid).spriteFrame = __gx_entities(eid).spriteFrame + 1
        If __gx_entities(eid).spriteFrame > __gx_entities(eid).seqFrames Then
            __gx_entities(eid).spriteFrame = 1
        End If
    End Sub

    Sub GXEntityFrameSet (eid As Integer, seq As Integer, frame As Integer)
        __gx_entities(eid).spriteSeq = seq
        __gx_entities(eid).spriteFrame = frame
        __gx_entities(eid).prevFrame = frame - 1
    End Sub

    'SUB GXEntityImageSet (eid AS INTEGER, filename AS STRING)
    '    __gx_entities(eid).image = __GX_ImageLoad(filename)
    'END SUB

    Sub GXEntityType (eid As Integer, etype As Integer)
        __gx_entities(eid).type = etype
    End Sub

    Function GXEntityType (eid As Integer)
        GXEntityType = __gx_entities(eid).type
    End Function


    ' TODO: Replace this implementation with a hash table lookup so we can scale
    '       efficiently when managing very large numbers of entities
    ' ----------------------------------------------------------------------------
    Function GXEntityUID$ (eid As Integer)
        GXEntityUID = __GX_UID(GXTYPE_ENTITY, eid)
    End Function

    Function GXFontUID$ (fid As Integer)
        GXFontUID = __GX_UID(GXTYPE_FONT, fid)
    End Function

    ' TODO: Expand this concept to lookup any GX object id by its user assigned id
    '       Maybe not "any" GX object... need to decide which ones would be applicable
    Function GX (uid As String * 10)
        GX = 0
        Dim i As Integer
        For i = 1 To UBound(__gx_objects)
            If RTrim$(__gx_objects(i).uid) = uid Then
                GX = __gx_objects(i).id
                Exit Function
            End If
        Next i
    End Function

    Sub __GX (uid As String * 10, id As Integer, otype As Integer)
        Dim count As Integer
        count = UBound(__gx_objects) + 1
        ReDim _Preserve __gx_objects(count) As GXObject
        __gx_objects(count).uid = uid
        __gx_objects(count).id = id
        __gx_objects(count).type = otype
    End Sub

    Function __GX_UID$ (otype As Integer, id As Integer)
        Dim uid As String
        Dim i As Integer
        For i = 1 To UBound(__gx_objects)
            If __gx_objects(i).type = otype Then
                If __gx_objects(i).id = id Then
                    uid = RTrim$(__gx_objects(i).uid)
                End If
            End If
        Next i
        __GX_UID = uid
    End Function
    ' ----------------------------------------------------------------------------


    Sub GXEntityApplyGravity (eid As Integer, gravity As Integer)
        __gx_entities(eid).applyGravity = gravity
        __gx_entities(eid).jumpstart = GXFrame
    End Sub

    Function GXEntityApplyGravity (eid As Integer)
        GXEntityApplyGravity = __gx_entities(eid).applyGravity
    End Function

    Sub GXEntityCollisionOffset (eid As Integer, left As Integer, top As Integer, right As Integer, bottom As Integer)
        __gx_entities(eid).coLeft = left
        __gx_entities(eid).coTop = top
        __gx_entities(eid).coRight = right
        __gx_entities(eid).coBottom = bottom
    End Sub

    Function GXEntityCollisionOffsetLeft (eid As Integer)
        GXEntityCollisionOffsetLeft = __gx_entities(eid).coLeft
    End Function

    Function GXEntityCollisionOffsetTop (eid As Integer)
        GXEntityCollisionOffsetTop = __gx_entities(eid).coTop
    End Function

    Function GXEntityCollisionOffsetRight (eid As Integer)
        GXEntityCollisionOffsetRight = __gx_entities(eid).coRight
    End Function

    Function GXEntityCollisionOffsetBottom (eid As Integer)
        GXEntityCollisionOffsetBottom = __gx_entities(eid).coBottom
    End Function


    Function GXPlayerCreate (eid As Integer)
        __gx_player_count = __gx_player_count + 1
        ReDim _Preserve __gx_players(__gx_player_count) As GXPlayer
        ReDim _Preserve __gx_player_keymap(10, __gx_player_count) As GXAction
        __gx_players(__gx_player_count).eid = eid
        __gx_players(__gx_player_count).walkSpeed = 60
        __gx_players(__gx_player_count).runSpeed = 120
        __gx_players(__gx_player_count).jumpSpeed = 120
        GXPlayerCreate = __gx_player_count
    End Function

    Sub GXPlayerMoveSpeed (pid As Integer, moveSpeed As Integer)
        __gx_players(pid).walkSpeed = moveSpeed
    End Sub

    Sub GXPlayerJumpSpeed (pid As Integer, jumpSpeed As Integer)
        __gx_players(pid).jumpSpeed = jumpSpeed
    End Sub

    Sub GXPlayerActionKey (pid As Integer, actionType As Integer, k As Long)
        __gx_player_keymap(actionType, pid).type = actionType
        __gx_player_keymap(actionType, pid).diDeviceType = GXDEVICE_KEYBOARD
        __gx_player_keymap(actionType, pid).diDeviceId = 1
        __gx_player_keymap(actionType, pid).diInputType = GXDEVICE_BUTTON
        __gx_player_keymap(actionType, pid).diInputId = k
        __gx_player_keymap(actionType, pid).diInputValue = -1
    End Sub

    Function GXPlayerActionKey (pid As Integer, actionType As Integer)
        If __gx_player_keymap(actionType, pid).diDeviceType = GXDEVICE_KEYBOARD Then
            GXPlayerActionKey = __gx_player_keymap(actionType, pid).diInputId
        Else
            GXPlayerActionKey = 0
        End If
    End Function

    Sub GXPlayerActionDisabled (pid As Integer, actionType As Integer, disabled As Integer)
        __gx_player_keymap(actionType, pid).disabled = disabled
    End Sub

    Function GXPlayerActionDisabled (pid As Integer, actionType As Integer)
        GXPlayerActionDisabled = __gx_player_keymap(actionType, pid).disabled
    End Function

    Sub GXPlayerActionInput (pid As Integer, actionType As Integer, di As GXDeviceInput)
        If di.deviceId = 0 Then
            ' Get the currently set device input values
            __GX_ActionInput __gx_player_keymap(actionType, pid), di
        Else
            ' Set the device input
            __gx_player_keymap(actionType, pid).type = actionType
            __gx_player_keymap(actionType, pid).diDeviceId = di.deviceId
            __gx_player_keymap(actionType, pid).diDeviceType = di.deviceType
            __gx_player_keymap(actionType, pid).diInputId = di.inputId
            __gx_player_keymap(actionType, pid).diInputType = di.inputType
            __gx_player_keymap(actionType, pid).diInputValue = di.inputValue
        End If
    End Sub

    Sub GXPlayerMoveKey (pid As Integer, action As Integer, k As Long, animationSeq As Integer, animationSpeed As Integer)
        Dim di As GXDeviceInput
        GXKeyInput k, di
        GXPlayerActionInput pid, action, di
        GXPlayerActionAnimationSeq pid, action, animationSeq
        GXPlayerActionAnimationSpeed pid, action, animationSpeed
    End Sub

    Sub GXPlayerMoveInput (pid As Integer, action As Integer, di As GXDeviceInput, animationSeq As Integer, animationSpeed As Integer)
        GXPlayerActionInput pid, action, di
        GXPlayerActionAnimationSeq pid, action, animationSeq
        GXPlayerActionAnimationSpeed pid, action, animationSpeed
    End Sub

    Sub __GX_ActionInput (a As GXAction, di As GXDeviceInput)
        di.deviceId = a.diDeviceId
        di.deviceType = a.diDeviceType
        di.inputId = a.diInputId
        di.inputType = a.diInputType
        di.inputValue = a.diInputValue
    End Sub

    Function GXDeviceInputTest (di As GXDeviceInput)
        Dim dcount As Integer
        dcount = _Devices

        If di.deviceId < 1 Or di.deviceId > dcount Then
            GXDeviceInputTest = GX_FALSE
            Exit Function
        End If

        Dim result As Integer
        Dim dactive As Integer
        dactive = _DeviceInput(di.deviceId)

        If di.inputType = GXDEVICE_BUTTON Then
            'If _Button(di.inputId) = di.inputValue Then
            '    result = GX_TRUE
            'End If
            If di.deviceType = GXDEVICE_KEYBOARD Then
                result = __GX_DeviceKeyDown(di.inputId)
            Else
                If _Button(di.inputId) = di.inputValue Then
                    result = GX_TRUE
                End If
            End If

        ElseIf di.inputType = GXDEVICE_AXIS Then
            If _Axis(di.inputId) = di.inputValue Then
                result = GX_TRUE
            End If
        End If

        GXDeviceInputTest = result
    End Function

    Function __GX_DeviceKeyDown (inputId As Integer)
        Dim k As KeyEntry
        k = __gx_keymap(inputId)

        Dim result As Integer
        result = GX_FALSE
        If _KeyDown(k.value) Then
            result = GX_TRUE
        ElseIf k.shift <> 0 Then
            If _KeyDown(k.shift) Then result = GX_TRUE
        End If

        __GX_DeviceKeyDown = result
    End Function

    Sub GXPlayerActionAnimationSeq (pid As Integer, actionType As Integer, animationSeq As Integer)
        __gx_player_keymap(actionType, pid).animationSeq = animationSeq
    End Sub

    Function GXPlayerActionAnimationSeq (pid As Integer, actionType As Integer)
        GXPlayerActionAnimationSeq = __gx_player_keymap(actionType, pid).animationSeq
    End Function

    Sub GXPlayerActionAnimationMode (pid As Integer, actionType As Integer, animationMode As Integer)
        __gx_player_keymap(actionType, pid).animationMode = animationMode
    End Sub

    Function GXPlayerActionAnimationMode (pid As Integer, actionType As Integer)
        GXPlayerActionAnimationMode = __gx_player_keymap(actionType, pid).animationMode
    End Function

    Sub GXPlayerActionAnimationSpeed (pid As Integer, actionType As Integer, animationSpeed As Integer)
        __gx_player_keymap(actionType, pid).animationSpeed = animationSpeed
    End Sub

    Function GXPlayerActionAnimationSpeed (pid As Integer, actionType As Integer)
        GXPlayerActionAnimationSpeed = __gx_player_keymap(actionType, pid).animationSpeed
    End Function

    ' Sets the fullscreen status.
    ' When set to true the scene content will stretch to fill the entire screen.
    ' This value is set to false by default.
    Sub GXFullScreen (fullscreen As Integer)
        If fullscreen Then
            _FullScreen _SquarePixels
        Else
            _FullScreen _Off
        End If
        __gx_scene.fullscreen = fullscreen
    End Sub

    ' Sets the fullscreen status.
    Function GXFullScreen
        GXFullScreen = __gx_scene.fullscreen
    End Function

    ' Adds a new background image to the current scene.  Multiple background images may be added to the scene.
    ' Background images are displayed in layers based on the order they are added.
    ' One of the following modes must be specified:
    '   GXBG_STRETCH - Stretch the background image to the size of the scene.
    '   GXBG_SCROLL  - Fit the height of the background image to the size of the screen.
    '                   Scroll the horizontal position relative to the position on the map.
    '   GXBG_WRAP    - Continuously wrap the background as the scene is moved horizontally.
    Function GXBackgroundAdd (imageFilename As String, mode As Integer)
        __gx_bg_count = __gx_bg_count + 1
        ReDim _Preserve __gx_bg(__gx_bg_count) As GXBackground
        __gx_bg(__gx_bg_count).mode = mode
        If mode = GXBG_WRAP Then
            Dim img As Long
            img = __GX_ImageLoad(imageFilename)
            Dim w As Integer, h As Integer
            w = _Width(img)
            h = _Height(img)
            Dim newImage As Long
            'newImage = _NewImage(w * 2, h, 32)
            newImage = __GX_HardwareImage(w * 2, h)
            _PutImage (0, 0)-(w, h), img, newImage
            _PutImage (w, 0)-(w * 2, h), img, newImage
            _FreeImage img
            __gx_bg(__gx_bg_count).image = newImage
        Else
            __gx_bg(__gx_bg_count).image = __GX_ImageLoad(imageFilename)
        End If
        GXBackgroundAdd = __gx_bg_count
    End Function

    Sub GXBackgroundY (gi As Integer, y As Integer)
        __gx_bg(gi).y = y
    End Sub

    Sub GXBackgroundHeight (gi As Integer, height As Integer)
        __gx_bg(gi).height = height
    End Sub

    ' Removes all background images from the scene.
    Sub GXBackgroundClear
        __gx_bg_count = 0
        ReDim __gx_bg(0) As GXBackground
    End Sub

    Sub __GX_BackgroundDraw (bi As Integer)
        Dim factor, xfactor
        Dim w As Long, h As Long, x As Long, y As Long

        If __gx_bg(bi).mode = GXBG_STRETCH Then
            'Print __gx_bg(bi).image, __gx_scene.image
            'Dim a: Input a
            _PutImage , __gx_bg(bi).image, __gx_scene.image

        ElseIf __gx_bg(bi).mode = GXBG_SCROLL Then
            factor = GXSceneWidth / GXSceneHeight
            h = _Height(__gx_bg(bi).image)
            w = h * factor
            'x = gx_bg(bi).xpos
            xfactor = GXSceneX / (GXMapColumns * GXTilesetWidth)
            x = xfactor * (_Width(__gx_bg(bi).image) - w)
            _PutImage , __gx_bg(bi).image, __gx_scene.image, (x, 0)-(x + w, h)

        ElseIf __gx_bg(bi).mode = GXBG_WRAP Then
            h = __gx_bg(bi).height
            w = GXSceneWidth
            y = __gx_bg(bi).y
            x = (GXSceneX Mod _Width(__gx_bg(bi).image)) / 2
            _PutImage (0, y)-(GXSceneWidth, y + h), __gx_bg(bi).image, __gx_scene.image, (x, 0)-(x + w, _Height(__gx_bg(bi).image))
            __gx_bg(bi).x = x + 1
            If __gx_bg(bi).x > _Width(__gx_bg(bi).image) / 2 Then
                __gx_bg(bi).x = 0
            End If
        End If
    End Sub

    ' Gets the embedded flag for the scene.
    Sub GXSceneEmbedded (embedded As Integer)
        __gx_scene.embedded = embedded
    End Sub

    ' Sets the embedded flag for the scene.
    ' The embedded flag should be set to true when the game loop is being
    ' managed manually or by an external framework (e.g. QB64 Inform).
    ' This value is set to false by default.
    Function GXSceneEmbedded
        GXSceneEmbedded = __gx_scene.embedded
    End Function

    Function __GX_HardwareImage& (iwidth As Integer, iheight As Integer)
        Dim newImage As Long
        newImage = _NewImage(iwidth, iheight, 32)
        'If Not GXSceneEmbedded Then
        If GXHardwareAcceleration Then
            Dim hwImage As Long
            hwImage = _CopyImage(newImage, 33)
            _FreeImage newImage
            __GX_HardwareImage = hwImage
        Else
            __GX_HardwareImage = newImage
        End If
    End Function

    ' Creates a new scene with the specified pixel width and height.
    Sub GXSceneCreate (swidth As Integer, sheight As Integer)
        __gx_scene.width = swidth
        __gx_scene.height = sheight

        If Not GXSceneEmbedded Then
            'If GXHardwareAcceleration Then
            Dim oldimg As Long
            oldimg = _ScreenImage
            Screen _NewImage(GXSceneWidth, GXSceneHeight, 32)
            'Dim hwImage As Long
            'hwImage = __GX_HardwareImage(GXSceneWidth, GXSceneHeight)
            'Screen hwImage
            _FreeImage oldimg
        End If

        If __gx_scene.image Then _FreeImage __gx_scene.image
        'If Not GXSceneEmbedded Then
        If GXHardwareAcceleration Then
            __gx_scene.image = __GX_HardwareImage(GXSceneWidth, GXSceneHeight)
        Else
            __gx_scene.image = _NewImage(GXSceneWidth, GXSceneHeight, 32)
        End If

        'If Not GXSceneEmbedded Then
        If GXHardwareAcceleration Then
            If Not __gx_img_blank Then __gx_img_blank = __GX_HardwareImage(1, 1)
        Else
            If Not __gx_img_blank Then __gx_img_blank = _NewImage(1, 1, 32)
        End If

        __gx_scene.scaleX = 1
        __gx_scene.scaleY = 1

        __GX_CustomEvent GXEVENT_INIT

    End Sub

    ' Scale the scene to the specified window size.
    Sub GXSceneWindowSize (swidth As Integer, sheight As Integer)
        Dim oldimg As Long
        oldimg = _ScreenImage
        Screen _NewImage(swidth, sheight, 32)
        'SCREEN __GX_HardwareImage(swidth, sheight)
        _FreeImage oldimg
        __gx_scene.scaleX = swidth / GXSceneWidth
        __gx_scene.scaleY = sheight / GXSceneHeight
    End Sub

    ' Scale the scene by the specified scale factor.
    Sub GXSceneScale (scale As Single)
        __gx_scene.scaleX = scale
        __gx_scene.scaleY = scale
        If Not GXSceneEmbedded Then
            Dim oldimg As Long
            oldimg = _ScreenImage
            Screen _NewImage(GXSceneWidth * __gx_scene.scaleX, GXSceneHeight * __gx_scene.scaleY, 32)
            'SCREEN __GX_HardwareImage(gx_scene.width * gx_scene.scaleX, gx_scene.height * gx_scene.scaleY)
            _FreeImage oldimg
        End If
    End Sub

    ' Resize the scene with the specified pixel width and height.
    Sub GXSceneResize (swidth As Integer, sheight As Integer)
        __gx_scene.width = swidth
        __gx_scene.height = sheight

        If __gx_scene.image Then _FreeImage (__gx_scene.image)
        __gx_scene.image = _NewImage(GXSceneWidth, GXSceneHeight, 32)
        'gx_scene.image = __GX_HardwareImage(gx_scene.width, gx_scene.height)

        If Not GXSceneEmbedded Then
            Dim oldimg As Long
            oldimg = _ScreenImage
            Screen _NewImage(GXSceneWidth, GXSceneHeight, 32)
            'SCREEN __GX_HardwareImage(gx_scene.width, gx_scene.height)
            _FreeImage oldimg
        End If
        __GX_UpdateSceneSize
    End Sub

    Sub GXSceneDestroy
        _Delay .1
        GXSceneStop
        _FreeImage __gx_scene.image
        _FreeImage __gx_img_blank
        __gx_scene.image = 0
        __gx_img_blank = 0
        Dim i As Integer
        For i = 0 To __gx_image_count - 1
            _FreeImage __gx_images(i).id
        Next i
        __gx_image_count = 0
        ReDim __gx_images(0) As GXImage
    End Sub

    Sub __GX_CustomEvent (eventType As Integer)
        Dim e As GXEvent
        e.event = eventType
        GXOnGameEvent e
    End Sub

    Sub GXCustomDraw (eventType As Integer, enabled As Integer)
        __gx_draw_events(eventType) = enabled
    End Sub

    Function GXCustomDraw (eventType As Integer)
        GXCustomDraw = __gx_draw_events(eventType)
    End Function

    Sub __GX_CustomDrawEvent (eventType As Integer)
        If Not GXHardwareAcceleration Then _Dest __gx_scene.image
        __GX_CustomEvent eventType
        If Not GXHardwareAcceleration Then _Dest 0

        'If GXSceneEmbedded Then
        '    _Dest __gx_scene.image
        '    __GX_CustomEvent eventType
        '    If GXDebug And eventType = GXEVENT_DRAWMAP Then __GX_DebugMapTile
        '    _Dest 0
        'Else
        '    If GXCustomDraw(eventType) Or (GXDebug And GXEVENT_DRAWMAP) Or (GXDebug And GXEVENT_DRAWSCREEN) Then
        '        If __gx_scene.customImage = 0 Then __gx_scene.customImage = _NewImage(GXSceneWidth, GXSceneHeight, 32)
        '        If __gx_img_blank_s = 0 Then __gx_img_blank_s = _NewImage(1, 1, 32)
        '        _DontBlend __gx_scene.customImage
        '        _PutImage , __gx_img_blank_s, __gx_scene.customImage
        '        _Blend __gx_scene.customImage
        '        _Dest __gx_scene.customImage
        '    End If
        '    __GX_CustomEvent eventType
        '    If GXCustomDraw(eventType) Or (GXDebug And GXEVENT_DRAWMAP) Or (GXDebug And GXEVENT_DRAWSCREEN) Then
        '        If GXDebug Then
        '            If eventType = GXEVENT_DRAWMAP Then __GX_DebugMapTile
        '            If eventType = GXEVENT_DRAWSCREEN Then __GX_DebugEntities
        '        End If
        '        _Dest 0
        '        Dim tmpImage As Long
        '        tmpImage = _CopyImage(__gx_scene.customImage, 33)
        '        _PutImage , tmpImage, __gx_scene.image
        '        _FreeImage tmpImage
        '    End If
        'End If


    End Sub

    'Sub __GX_DebugEntities
    '    Dim e As GXEntity
    '    Dim ei As Integer
    '    For ei = 1 To __gx_entity_count
    '        e = __gx_entities(ei)
    '        If e.screen = 0 Then
    '            If __GX_RectCollide(e.x, e.y, e.width, e.height, GXSceneX, GXSceneY, GXSceneWidth, GXSceneHeight) Then
    '                Dim x, y As Long
    '                If e.screen = 1 Then
    '                    x = e.x
    '                    y = e.y
    '                Else
    '                    x = e.x - GXSceneX
    '                    y = e.y - GXSceneY
    '                End If
    '                __GX_DebugEntity e, x, y
    '            End If
    '        End If
    '    Next ei
    'End Sub


    ' Returns the current frame rate (expressed in frames-per-second or FPS).
    Sub GXFrameRate (frameRate As Integer)
        __gx_framerate = frameRate
    End Sub

    ' Sets current frame rate (expressed in frames-per-second or FPS).
    ' The default value for the frame rate is 60
    Function GXFrameRate
        GXFrameRate = __gx_framerate
    End Function

    ' Returns the current frame.
    ' This is a frame counter that starts when GXSceneStart is called.
    ' It is initially set to zero and is incremented on each frame.
    Function GXFrame
        GXFrame = __gx_scene.frame
    End Function

    ' Draw the scene.
    ' This method is called automatically when GX is managing the event/game loop.
    ' Call this method for each page draw event when the event/game loop is being
    ' handled externally.
    Sub GXSceneDraw
        If __gx_map_loading Then Exit Sub
        Dim ei As Integer
        Dim ei2 As Integer
        Dim frame As Integer
        frame = __gx_scene.frame Mod GXFrameRate + 1

        ' If the screen has been resized, resize the destination screen image
        If _Resize And Not GXSceneEmbedded Then
            '_FREEIMAGE _SOURCE
            'SCREEN _NEWIMAGE(_RESIZEWIDTH, _RESIZEHEIGHT, 32)
            GXSceneWindowSize _ResizeWidth, _ResizeHeight
        End If

        ' Clear the background
        _DontBlend __gx_scene.image
        _PutImage , __gx_img_blank, __gx_scene.image
        _Blend __gx_scene.image

        ' Draw background images, if present
        Dim bi As Integer
        For bi = 1 To __gx_bg_count
            __GX_BackgroundDraw bi
        Next bi

        'Call out to any custom screen drawing
        __GX_CustomDrawEvent GXEVENT_DRAWBG

        'Print "before draw map"
        ' Draw the map tiles
        GXMapDraw
        If GXDebug Then __GX_DebugMapTile

        'Call out to any custom screen drawing
        __GX_CustomDrawEvent GXEVENT_DRAWMAP

        ' "before draw entities"
        ' Draw the entities
        Dim e As GXEntity
        For ei = 1 To __gx_entity_count
            e = __gx_entities(ei)
            If e.screen = 0 Then
                If __GX_RectCollide(e.x, e.y, e.width, e.height, GXSceneX, GXSceneY, GXSceneWidth, GXSceneHeight) Then
                    'Print ei, e.image
                    'Dim a: Input a
                    __GX_EntityDraw e
                End If
            End If
            If e.animate > 0 Then
                If frame Mod (GXFrameRate / e.animate) = 0 Then
                    GXEntityFrameNext ei
                End If
            End If
        Next ei

        ' "before screen entities"
        ' Draw the screen entities which should appear on top of the other game entities
        ' and have a fixed position
        For ei2 = 1 To __gx_entity_count
            If __gx_entities(ei2).screen = 1 Then
                __GX_EntityDraw __gx_entities(ei2)
            End If
        Next ei2

        'Call out to any custom screen drawing
        __GX_CustomDrawEvent GXEVENT_DRAWSCREEN
        If GXDebug Then __GX_DebugFrameRate

        ' Copy the background image to the screen
        __GX_CustomEvent GXEVENT_PAINTBEFORE
        _DontBlend
        _PutImage , __gx_scene.image
        _Blend
        __GX_CustomEvent GXEVENT_PAINTAFTER
    End Sub

    ' Moves the scene position by the number of pixels specified by the dx and dy values.
    ' The default position for a scene is (0,0). Negative x and y values are valid.
    ' A non-zero value for dx will move the scene by the number of pixels specified to the right or left.
    ' A non-zero value for dy will move the scene by the number of pixels specified up or down.
    Sub GXSceneMove (dx As Long, dy As Long)
        __gx_scene.x = GXSceneX + dx
        __gx_scene.y = GXSceneY + dy
    End Sub

    ' Positions the scene at the specified x and y coordinates.
    ' The default position for a scene is (0,0). Negative x and y values are valid.
    Sub GXScenePos (x As Long, y As Long)
        __gx_scene.x = x
        __gx_scene.y = y
    End Sub

    ' Returns the scene's current y position.
    Function GXSceneX
        GXSceneX = __gx_scene.x
    End Function

    ' Returns the scene's current y position.
    Function GXSceneY
        GXSceneY = __gx_scene.y
    End Function

    ' Returns the scene width.
    Function GXSceneWidth
        GXSceneWidth = __gx_scene.width
    End Function

    ' Returns the scene height.
    Function GXSceneHeight
        GXSceneHeight = __gx_scene.height
    End Function

    ' Returns the number of tile columns that can be displayed within the scene.
    ' This value will be zero unless a tiled map has been created or loaded.
    Function GXSceneColumns
        GXSceneColumns = __gx_scene.columns
    End Function

    ' Returns the number of tile rows that can be displayed within the scene.
    ' This value will be zero unless a tiled map has been created or loaded.
    Function GXSceneRows
        GXSceneRows = __gx_scene.rows
    End Function

    ' Start the game loop.
    ' This method will not return control to the calling program until the
    ' game loop is interrupted with the GXSceneStop method.
    ' Game events will be sent to the GXOnGameEvent method during the game
    ' loop execution.
    Sub GXSceneStart

        __gx_scene.frame = 0
        __gx_scene.active = 1

        'DIM dcount AS INTEGER
        'dcount = _DEVICES
        Do
            _Limit GXFrameRate

            ' Update the scene
            GXSceneUpdate

            ' Redraw the scene
            GXSceneDraw
            _Display

        Loop While __gx_scene.active
    End Sub

    Sub GXSceneUpdate
        __gx_scene.frame = __gx_scene.frame + 1

        ' Capture mouse events when not embedded
        If Not GXSceneEmbedded Then
            Dim mi As Long
            mi = _MouseInput
        End If

        ' Call custom game update logic
        __GX_CustomEvent GXEVENT_UPDATE

        ' Perform any movement for registered player entities
        Dim i As Integer
        For i = 1 To __gx_player_count
            __GX_PlayerAction i
        Next i

        ' Check for entity movement and collisions
        ' TODO: filter out non-moving entities
        __GX_SceneMoveEntities

        ' Perform any auto-scene moves
        Dim sx As Long, sy As Long
        IF __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER OR _
           __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_X OR _
           __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_X_POS OR _
           __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_X_NEG THEN
            sx = (GXEntityX(__gx_scene.followEntity) + GXEntityWidth(__gx_scene.followEntity) / 2) - GXSceneWidth / 2
            IF (sx < GXSceneX AND __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_X_POS) OR _
               (sx > GXSceneX AND __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_X_NEG) THEN
                ' don't move the scene
            Else
                GXScenePos sx, GXSceneY
            End If
        End If
        IF __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER OR _
           __gx_scene.followMode = GXSCENE_FOLLOW_ENTITY_CENTER_Y THEN
            sy = (GXEntityY(__gx_scene.followEntity) + GXEntityHeight(__gx_scene.followEntity) / 2) - GXSceneHeight / 2
            GXScenePos GXSceneX, sy
        End If

        ' Check the scene move constraints
        If __gx_scene.constrainMode = GXSCENE_CONSTRAIN_TO_MAP Then
            Dim mwidth As Integer
            Dim mheight As Integer
            mwidth = GXMapColumns * GXTilesetWidth
            mheight = GXMapRows * GXTilesetHeight
            sx = GXSceneX
            If sx < 0 Then
                sx = 0
            ElseIf sx + GXSceneWidth > mwidth Then
                sx = mwidth - GXSceneWidth
                If sx < 0 Then sx = 0
            End If

            sy = GXSceneY
            If sy < 0 Then
                sy = 0
            ElseIf sy + GXSceneHeight > mheight Then
                sy = mheight - GXSceneHeight
                If sy < 0 Then sy = 0
            End If
            GXScenePos sx, sy
        End If

        If Not GXSceneEmbedded Then
            While _MouseInput
                ' Flush mouse event buffer
            Wend
        End If
    End Sub

    ' TODO: revisit "follow" wording.
    Sub GXSceneFollowEntity (eid As Integer, mode As Integer)
        __gx_scene.followEntity = eid
        __gx_scene.followMode = mode
    End Sub

    Sub GXSceneConstrain (mode As Integer)
        __gx_scene.constrainMode = mode
    End Sub

    Sub __GX_SceneMoveEntities
        Dim frameFactor As Double
        frameFactor = 1 / GXFrameRate

        'DIM e AS GXEntity
        Dim eid As Integer, ci As Integer
        ReDim centities(0) As Integer
        Dim ecount As Integer
        For eid = 1 To __gx_entity_count
            If __gx_entities(eid).screen = 0 Then
                __GX_SceneMoveEntity eid

                '' Check for entity collisions
                'ecount = __GX_EntityCollision(eid, 0, 0, centities())
                'FOR ci = 1 TO ecount
                '    DIM ee AS GXEvent
                '    ee.event = GXEVENT_COLLISION_ENTITY
                '    ee.entity = eid
                '    ee.collisionEntity = centities(ci)
                '    GXOnGameEvent ee
                'NEXT ci

                '' Move the entity
                'IF __gx_entities(eid).applyGravity = 1 THEN
                '    DIM tpos AS GXPosition
                '    DIM centity AS INTEGER
                '    DIM tmove AS INTEGER
                '    tmove = __GX_EntityTestMove(eid, 0, 1, tpos, centity)
                '    centity = 0
                '    IF (__gx_entities(eid).jumping = 1 AND __gx_entities(eid).vy < 0) OR tmove THEN
                '        IF __gx_entities(eid).jumpstart = 0 THEN
                '            __gx_entities(eid).jumpstart = GXFrame - 45
                '        ELSE
                '            ' calculate the number of seconds since the gravity started being applied
                '            DIM t
                '            t = (GXFrame - __gx_entities(eid).jumpstart) / GXFrameRate
                '            __gx_entities(eid).vy = __gx_entities(eid).vy + 9.8 * t ^ 2 / 2
                '        END IF
                '        'ELSE
                '    END IF

                '    ' test to see if we have hit the block or entity above or below
                '    tmove = __GX_EntityTestMove(eid, 0, __gx_entities(eid).vy * frameFactor, tpos, centity)
                '    IF tmove = 0 THEN
                '        IF __gx_entities(eid).vy < 0 THEN

                '            ' if we hit a block above, then reverse the current velocity
                '            ' and make sure the entity does not move above the collision offset
                '            __gx_entities(eid).vy = -__gx_entities(eid).vy
                '            '                    gx_entities(eid).jumpspeed = 0
                '            IF (centity) THEN
                '                __gx_entities(eid).y = GXEntityY(centity) + GXEntityHeight(centity) - GXEntityCollisionOffsetTop(eid)
                '            ELSE
                '                __gx_entities(eid).y = GXTilesetHeight * (tpos.y + 1) - GXEntityCollisionOffsetTop(eid)
                '            END IF

                '        ELSEIF __gx_entities(eid).vy > 0 THEN

                '            ' if we hit a block below, then zero out the jump speed and end the "fall"
                '            ' and make sure the entity does not move below the collision offset
                '            __gx_entities(eid).jumpstart = 0
                '            __gx_entities(eid).jumping = 0
                '            __gx_entities(eid).vy = 0

                '            IF (centity) THEN
                '                __gx_entities(eid).y = GXEntityY(centity) - GXEntityHeight(eid)
                '            ELSE
                '                __gx_entities(eid).y = GXTilesetHeight * (tpos.y) - (GXEntityHeight(eid) - GXEntityCollisionOffsetBottom(eid))
                '            END IF
                '        END IF

                '        'END IF
                '    END IF

                'END IF ' end if apply gravity

                ' apply the move vector to the entity's position
                If __gx_entities(eid).vx Then
                    __gx_entities(eid).x = __gx_entities(eid).x + __gx_entities(eid).vx * frameFactor
                End If
                If __gx_entities(eid).vy Then
                    __gx_entities(eid).y = __gx_entities(eid).y + __gx_entities(eid).vy * frameFactor
                End If
            End If
        Next eid
    End Sub


    Sub __GX_SceneMoveEntity (eid As Long)

        Dim tpos As GXPosition
        Dim centity As Integer
        Dim tmove As Integer
        Dim testx As Integer
        Dim testy As Integer

        ' Test upward movement
        If GXEntityVY(eid) < 0 Then
            testy = GXEntityVY(eid) / GXFrameRate
            If testy > -1 Then testy = -1
            tmove = __GX_EntityTestMove(eid, 0, testy, tpos, centity)
            If tmove = 0 Then
                If GXEntityApplyGravity(eid) Then
                    ' reverse the motion
                    GXEntityVY eid, GXEntityVY(eid) * -.5
                Else
                    ' stop the motion
                    GXEntityVY eid, 0
                End If

                ' don't let the entity pass into the collision entity or tile
                If centity > 0 Then
                    GXEntityPos eid, GXEntityX(eid), GXEntityY(centity) - GXEntityCollisionOffsetBottom(centity) + GXEntityHeight(centity) - GXEntityCollisionOffsetTop(eid)
                Else
                    GXEntityPos eid, GXEntityX(eid), (tpos.y + 1) * GXTilesetHeight - GXEntityCollisionOffsetTop(eid)
                End If

            End If
        End If

        If Not GXEntityApplyGravity(eid) Then
            ' Test downward movement
            If GXEntityVY(eid) > 0 Then
                testy = GXEntityVY(eid) / GXFrameRate
                If testy < 1 Then testy = 1
                tmove = __GX_EntityTestMove(eid, 0, testy, tpos, centity)
                If tmove = 0 Then
                    ' stop the motion
                    GXEntityVY eid, 0

                    ' don't let the entity pass into the collision entity or tile
                    If centity > 0 Then
                        GXEntityPos eid, GXEntityX(eid), GXEntityY(centity) + GXEntityCollisionOffsetTop(centity) - GXEntityHeight(eid) + GXEntityCollisionOffsetBottom(eid)
                    End If
                    If tpos.y > -1 Then
                        GXEntityPos eid, GXEntityX(eid), tpos.y * GXTilesetHeight - GXEntityHeight(eid) + GXEntityCollisionOffsetBottom(eid)
                    End If
                End If
            End If

        Else

            ' Apply gravity
            testy = GXEntityVY(eid) / GXFrameRate
            If testy < 1 Then testy = 1
            tmove = __GX_EntityTestMove(eid, 0, testy, tpos, centity)
            If tmove = 1 Then
                ' calculate the number of seconds since the gravity started being applied
                Dim t
                t = (GXFrame - __gx_entities(eid).jumpstart) / GXFrameRate
                ' adjust the y velocity for gravity
                Dim g As Double
                g = __gx_gravity * t ^ 2 / 2
                If g < 1 Then g = 1
                __gx_entities(eid).vy = __gx_entities(eid).vy + g
                If GXEntityVY(eid) > __gx_terminal_velocity Then GXEntityVY eid, __gx_terminal_velocity

            ElseIf GXEntityVY(eid) >= 0 Then
                '__gx_entities(eid).jumpstart = GXFrame
                __gx_entities(eid).jumpstart = GXFrame
                If GXEntityVY(eid) <> 0 Then
                    'DIM vy: vy = GXEntityVY(eid) * -.1
                    'IF ABS(vy) < 20 THEN vy = 0
                    GXEntityVY eid, 0

                    ' don't let the entity fall through the collision entity or tile
                    If centity > 0 Then
                        GXEntityPos eid, GXEntityX(eid), GXEntityY(centity) + GXEntityCollisionOffsetTop(centity) - GXEntityHeight(eid) + GXEntityCollisionOffsetBottom(eid)
                        'END IF
                        'IF tpos.y > -1 THEN
                    Else
                        GXEntityPos eid, GXEntityX(eid), tpos.y * GXTilesetHeight - GXEntityHeight(eid) + GXEntityCollisionOffsetBottom(eid)
                    End If
                End If
            End If
        End If

        If GXEntityVX(eid) > 0 Then
            ' Test right movement
            testx = GXEntityVX(eid) / GXFrameRate
            If testx < 1 Then testx = 1
            tmove = __GX_EntityTestMove(eid, testx, 0, tpos, centity)
            If tmove = 0 Then
                ' stop the motion
                GXEntityVX eid, 0

                ' don't let the entity pass into the collision entity or tile
                If centity > 0 Then
                    GXEntityPos eid, GXEntityX(centity) + GXEntityCollisionOffsetLeft(centity) - GXEntityWidth(eid) + GXEntityCollisionOffsetRight(eid), GXEntityY(eid)
                End If
                If tpos.x > -1 Then
                    GXEntityPos eid, tpos.x * GXTilesetWidth - GXEntityWidth(eid) + GXEntityCollisionOffsetRight(eid), GXEntityY(eid)
                End If
            End If

        ElseIf GXEntityVX(eid) < 0 Then
            ' Test left movement
            testx = GXEntityVX(eid) / GXFrameRate
            If testx > -1 Then testx = -1
            tmove = __GX_EntityTestMove(eid, testx, 0, tpos, centity)
            If tmove = 0 Then
                ' stop the motion
                GXEntityVX eid, 0

                ' don't let the entity pass into the collision entity or tile
                If centity > 0 Then
                    GXEntityPos eid, GXEntityX(centity) + GXEntityWidth(centity) - GXEntityCollisionOffsetRight(centity) - GXEntityCollisionOffsetLeft(eid), GXEntityY(eid)
                End If
                If tpos.x > -1 Then
                    GXEntityPos eid, (tpos.x + 1) * GXTilesetWidth - GXEntityCollisionOffsetLeft(eid), GXEntityY(eid)
                End If
            End If
        End If

    End Sub


    ' Stop the game loop.
    ' This method will cause the game loop to end and return control to the calling program.
    Sub GXSceneStop
        __gx_scene.active = 0
    End Sub



    Sub GXMapCreate (columns As Integer, rows As Integer, layers As Integer)
        __gx_map.columns = columns
        __gx_map.rows = rows
        __gx_map.layers = layers
        __gx_map.version = 2

        Dim layerSize As Long
        layerSize = rows * columns
        ReDim __gx_map_layers(layerSize, layers) As GXMapTile
        ReDim __gx_map_layer_info(layers) As GXMapLayer
        Dim l As Integer
        For l = 1 To layers
            __gx_map_layer_info(l).id = l
        Next l
    End Sub

    Function GXMapColumns
        GXMapColumns = __gx_map.columns
    End Function

    Function GXMapRows
        GXMapRows = __gx_map.rows
    End Function

    Function GXMapLayers
        GXMapLayers = __gx_map.layers
    End Function

    Sub GXMapLayerVisible (layer As Integer, visible As Integer)
        __gx_map_layer_info(layer).hidden = Not visible
    End Sub

    Function GXMapLayerVisible (layer As Integer)
        GXMapLayerVisible = Not __gx_map_layer_info(layer).hidden
    End Function

    Sub GXMapLayerAdd
        ReDim _Preserve __gx_map_layer_info(GXMapLayers + 1) As GXMapLayer
        ReDim _Preserve __gx_map_layers(GXMapRows * GXMapColumns, GXMapLayers + 1) As GXMapTile
        __gx_map_layer_info(GXMapLayers + 1).id = GXMapLayers + 1
        __gx_map.layers = GXMapLayers + 1
    End Sub

    Sub GXMapLayerInsert (beforeLayer As Integer)
        If beforeLayer < 1 Or beforeLayer > GXMapLayers Then Exit Sub

        GXMapLayerAdd
        Dim layer As Integer
        Dim tile As Integer
        For layer = GXMapLayers To beforeLayer + 1 Step -1
            'gx_map_layer_info(layer) = gx_map_layer_info(layer - 1)
            For tile = 0 To GXMapRows * GXMapColumns
                __gx_map_layers(tile, layer) = __gx_map_layers(tile, layer - 1)
            Next tile
        Next layer
        Dim blankTile As GXMapTile
        For tile = 0 To GXMapRows * GXMapColumns
            __gx_map_layers(tile, beforeLayer) = blankTile
        Next tile
    End Sub

    Sub GXMapLayerRemove (removeLayer As Integer)
        If removeLayer < 1 Or removeLayer > GXMapLayers Or GXMapLayers < 2 Then Exit Sub

        Dim layer As Integer
        Dim tile As Integer
        For layer = removeLayer To GXMapLayers - 1
            For tile = 0 To GXMapRows * GXMapColumns
                __gx_map_layers(tile, layer) = __gx_map_layers(tile, layer + 1)
            Next tile
        Next layer

        ReDim _Preserve __gx_map_layer_info(GXMapLayers - 1) As GXMapLayer
        ReDim _Preserve __gx_map_layers(GXMapRows * GXMapColumns, GXMapLayers - 1) As GXMapTile
        __gx_map.layers = GXMapLayers - 1
    End Sub

    Sub GXMapResize (columns As Integer, rows As Integer)
        Dim tempMap(GXMapRows * GXMapColumns, GXMapLayers) As GXMapTile
        Dim m1 As _MEM: m1 = _Mem(__gx_map_layers())
        Dim m2 As _MEM: m2 = _Mem(tempMap())
        _MemCopy m1, m1.OFFSET, m1.SIZE To m2, m2.OFFSET
        _MemFree m1
        _MemFree m2

        ReDim __gx_map_layers(rows * columns, GXMapLayers) As GXMapTile

        Dim layer As Integer
        Dim row As Integer
        Dim column As Integer
        Dim maxColumns As Integer
        Dim maxRows As Integer
        If columns > GXMapColumns Then
            maxColumns = GXMapColumns
        Else
            maxColumns = columns
        End If
        If rows > GXMapRows Then
            maxRows = GXMapRows
        Else
            maxRows = rows
        End If

        Dim blankTile As GXMapTile
        For layer = 1 To GXMapLayers
            For row = 0 To maxRows - 1
                For column = 0 To maxColumns - 1
                    If column >= GXMapColumns Or row >= GXMapRows Then
                        __gx_map_layers(row * columns + column, layer) = blankTile
                    Else
                        __gx_map_layers(row * columns + column, layer) = tempMap(row * GXMapColumns + column, layer)
                    End If
                Next column
            Next row
        Next layer

        __gx_map.columns = columns
        __gx_map.rows = rows
    End Sub

    Sub GXMapDraw
        If GXMapRows < 1 Then Exit Sub

        Dim tpos As GXPosition
        Dim srow, scol, row, col
        Dim layer As Integer, li As Integer
        Dim xoffset, yoffset, pcol, prow As Long
        Dim t As Long, tx As Long, ty As Long
        Dim rowOffset As Long
        'DIM rowOffset AS DOUBLE, singleRowOffset AS DOUBLE
        Dim colOffset As Long

        xoffset = GXSceneX Mod GXTilesetWidth
        pcol = Fix(GXSceneX / GXTilesetWidth)
        If GXMapIsometric Then
            prow = Fix(GXSceneY / (GXTilesetWidth / 4))
            yoffset = GXSceneY Mod (GXTilesetWidth / 4)
        Else
            prow = Fix(GXSceneY / GXTilesetHeight)
            yoffset = GXSceneY Mod GXTilesetHeight
        End If

        'singleRowOffset = 0
        'IF GXMapIsometric THEN
        '    singleRowOffset = GXTilesetHeight - GXTilesetWidth / 4
        '    rowOffset = singleRowOffset
        'END IF

        For li = 1 To GXMapLayers
            If Not __gx_map_layer_info(li).hidden Then
                layer = __gx_map_layer_info(li).id

                srow = 0
                rowOffset = 0

                For row = prow To prow + GXSceneRows + 1 'TODO: currently rendering too many rows for isometric maps
                    scol = 0
                    If Not GXMapIsometric Then
                        colOffset = 0
                    Else
                        colOffset = 0 'GXTilesetWidth / 2
                        If row Mod 2 = 0 Then colOffset = GXTilesetWidth / 2
                    End If

                    If GXMapIsometric Then
                        'rowOffset = (row - prow + 1) * _ROUND(GXTilesetHeight - GXTilesetWidth / 4)
                        rowOffset = (row - prow + 1) * (GXTilesetHeight - GXTilesetWidth / 4)
                    End If

                    For col = pcol To pcol + GXSceneColumns + 1
                        'FOR layer = 1 TO gx_map.layers

                        t = GXMapTile(col, row, layer)
                        If t > 0 Then
                            Dim t1 As Integer: t1 = t
                            t = __GX_TileFrame(t)
                            GXTilesetPos t, tpos
                            tx = scol * GXTilesetWidth - xoffset - colOffset
                            ty = srow * GXTilesetHeight - yoffset - rowOffset
                            GXSpriteDraw GXTilesetImage, tx, ty, tpos.y, tpos.x, GXTilesetWidth, GXTilesetHeight, __gx_scene.image
                        End If
                        scol = scol + 1
                    Next col
                    srow = srow + 1
                    'IF GXMapIsometric THEN rowOffset = rowOffset + singleRowOffset '(GXTilesetHeight * .75)
                Next row

            End If ' layer is not hidden
        Next li


        ' Perform tile animation
        For t = 1 To GXTilesetColumns * GXTilesetRows
            __GX_TileFrameNext t
        Next t
    End Sub

    Sub GXMapTilePosAt (x As Integer, y As Integer, tpos As GXPosition)
        If Not GXMapIsometric Then
            tpos.x = Fix((x + GXSceneX) / GXTilesetWidth)
            tpos.y = Fix((y + GXSceneY) / GXTilesetHeight)
        Else
            Dim tileWidthHalf As Integer: tileWidthHalf = GXTilesetWidth / 2
            Dim tileHeightHalf As Integer: tileHeightHalf = GXTilesetHeight / 2
            Dim sx As Long: sx = x / tileWidthHalf

            Dim offset As Integer
            If sx Mod 2 = 1 Then
                offset = tileWidthHalf
            Else
                offset = 0
            End If

            tpos.y = (2 * y) / tileHeightHalf
            tpos.x = (x - offset) / GXTilesetWidth
        End If
    End Sub

    Function __GX_TileFrame (tileId)
        If tileId < 0 Or tileId > UBound(__gx_tileset_tiles) Then
            __GX_TileFrame = tileId
            Exit Function
        End If

        If __gx_tileset_tiles(tileId).animationId = 0 Then
            __GX_TileFrame = tileId
            Exit Function
        End If

        Dim currFrame As Integer
        currFrame = __gx_tileset_tiles(tileId).animationId
        If __gx_tileset_tiles(tileId).animationFrame > 0 Then
            currFrame = __gx_tileset_tiles(tileId).animationFrame
        End If

        __GX_TileFrame = __gx_tileset_animations(currFrame).tileId
    End Function


    Sub __GX_TileFrameNext (tileId As Long)
        If tileId < 0 Or tileId > UBound(__gx_tileset_tiles) Then Exit Sub
        If __gx_tileset_tiles(tileId).animationId = 0 Then Exit Sub

        Dim frame As Integer
        frame = GXFrame Mod GXFrameRate + 1

        Dim firstFrame As Integer
        firstFrame = __gx_tileset_tiles(tileId).animationId

        Dim animationSpeed As Integer
        animationSpeed = __gx_tileset_tiles(tileId).animationSpeed

        If frame Mod (GXFrameRate / animationSpeed) = 0 Then
            Dim currFrame As Integer
            currFrame = firstFrame
            If __gx_tileset_tiles(tileId).animationFrame > 0 Then
                currFrame = __gx_tileset_tiles(tileId).animationFrame
            End If

            Dim nextFrame As Integer
            nextFrame = __gx_tileset_animations(currFrame).nextFrame
            If nextFrame = 0 Then
                nextFrame = firstFrame
            End If

            __gx_tileset_tiles(tileId).animationFrame = nextFrame
        End If
    End Sub


    Sub GXMapTile (col As Integer, row As Integer, layer As Integer, tile As Integer)
        If tile < 0 Then Exit Sub
        'TODO: check for tile greater than max and exit early

        If (col >= 0 And col <= GXMapColumns And row >= 0 And row < GXMapRows) Then
            Dim mpos As Long
            mpos = row * GXMapColumns + col
            __gx_map_layers(mpos, layer).tile = tile
        End If
    End Sub

    Function GXMapTile (col As Integer, row As Integer, layer As Integer)
        If col < 0 Or col >= GXMapColumns Or row < 0 Or row >= GXMapRows Or layer > GXMapLayers Then
            GXMapTile = 0
        Else
            Dim mpos As Long
            mpos = row * GXMapColumns + col
            'IF mpos > UBOUND(gx_map_layers) THEN
            '    GXMapTile = 0
            '    EXIT SUB
            'END IF
            GXMapTile = __gx_map_layers(mpos, layer).tile
        End If
    End Function

    Function GXMapTileDepth (col As Integer, row As Integer)
        If col < 0 Or col >= GXMapColumns Or row < 0 Or row >= GXMapRows Then
            GXMapTileDepth = 0
        Else
            Dim layer As Integer
            For layer = GXMapLayers To 1 Step -1
                If GXMapTile(col, row, layer) > 0 Then
                    GXMapTileDepth = layer
                    Exit Function
                End If
            Next layer
            GXMapTileDepth = 0
        End If
    End Function

    Sub GXMapTileAdd (col As Integer, row As Integer, tile As Integer)
        If tile < 1 Then Exit Sub
        'TODO: check for tile greater than max and exit early

        If (col >= 0 And col <= GXMapColumns And row >= 0 And row <= GXMapRows) Then
            Dim layer As Integer
            For layer = 1 To GXMapLayers
                If GXMapTile(col, row, layer) = 0 Then
                    GXMapTile col, row, layer, tile
                    Exit Sub
                End If
            Next layer
        End If
    End Sub

    Sub GXMapTileRemove (col As Integer, row As Integer)
        If (col >= 0 And col <= GXMapColumns And row >= 0 And row < GXMapRows) Then
            Dim layer As Integer
            For layer = GXMapLayers To 1 Step -1
                If GXMapTile(col, row, layer) Then
                    GXMapTile col, row, layer, 0
                    Exit Sub
                End If
            Next layer
        End If
    End Sub

    Function GXMapVersion
        GXMapVersion = __gx_map.version
    End Function

    Sub GXMapSave (filename As String)
        Dim version As Integer
        Dim col As Integer
        Dim row As Integer
        Dim layer As Integer
        Dim tile As Integer
        Dim depth As Integer

        version = 2

        ' TODO: check to see if the tileset image has been removed before deleting
        '       the original file
        If _FileExists(filename) Then
            Kill filename
        End If
        Open filename For Binary As #1

        ' Save the map version
        Put #1, , version

        ' Save the map meta-data
        Put #1, , __gx_map.columns
        Put #1, , __gx_map.rows
        Put #1, , __gx_map.layers
        Put #1, , __gx_map.isometric

        ' Save the map tile layer data
        ' TODO: use a temp file strategy that will avoid file collisions
        '       or potentially read and write from memory instead
        Open "./tmp/layer" For Binary As #2
        Put #2, , __gx_map_layers()
        Close #2
        Open "./tmp/layer" For Binary As #2
        Dim lstr As String
        lstr = String$(LOF(2), " ")
        Get #2, , lstr
        Close #2
        Kill "./tmp/layer"
        lstr = _Deflate$(lstr)
        __GX_WriteString lstr

        ' Save the tilemap information
        __GX_TilesetSave

        Close #1
    End Sub

    Sub GXMapSaveWeb (filename As String)
        Dim version As Integer
        Dim col As Integer
        Dim row As Integer
        Dim layer As Integer
        Dim tile As Integer
        Dim depth As Integer

        version = 2

        ' TODO: check to see if the tileset image has been removed before deleting
        '       the original file
        If _FileExists(filename) Then
            Kill filename
        End If
        Open filename For Output As #1

        Print #1, "{"
        ' Save the map version
        Print #1, __JSL("version"); version; ","

        ' Save the map meta-data
        Print #1, __JSL("columns"); __gx_map.columns; ","
        Print #1, __JSL("rows"); __gx_map.rows; ","
        'Print #1, __JSL("layers"); __gx_map.layers; ","
        Print #1, __JSL("isometric"); __gx_map.isometric; ","


        ' Save the map tile layer data
        Print #1, __JSL("layers"); "["
        For layer = 1 To GXMapLayers
            Print #1, "[";
            For row = 0 To GXMapRows - 1
                For col = 0 To GXMapColumns - 1
                    Print #1, GXMapTile(col, row, layer);
                    If col = GXMapColumns - 1 And row = GXMapRows - 1 Then
                        'No comma
                    Else
                        Print #1, ",";
                    End If
                Next col
                Print #1, ""
            Next row
            If layer < GXMapLayers Then
                Print #1, "],"
            Else
                Print #1, "]"
            End If
        Next layer
        Print #1, "],"

        ' Save the tilemap information
        __GX_TilesetSaveWeb filename + ".png"

        Print #1, "}"
        Close #1
    End Sub

    Function __JSL$ (s As String)
        ' wrap the string in double quotes
        __JSL = Chr$(34) + s + Chr$(34) + ":"
    End Function

    ' Load the map
    Sub GXMapLoad (filename As String)
        __gx_map_loading = 1

        Open filename For Binary As #1

        ' Read the version
        Get #1, 1, __gx_map.version

        ' For backwards compatibility, add new versions of the
        ' load function to continue to support older maps
        Select Case GXMapVersion
            Case 1: __GX_MapLoadV1 filename
            Case 2: __GX_MapLoadV2 filename
        End Select

        Close #1

        __gx_map_loading = 0
    End Sub

    ' Load a version 2 map
    Sub __GX_MapLoadV2 (filename As String)
        Dim columns As Integer
        Dim rows As Integer
        Dim layers As Integer
        Dim col As Integer
        Dim row As Integer
        Dim layer As Integer
        Dim depth As Integer
        Dim tile As Integer

        Get #1, , columns
        Get #1, , rows
        Get #1, , layers
        Get #1, , __gx_map.isometric
        GXMapCreate columns, rows, layers
        '--------------------------------------------------------------
        ' temporary code - remove
        '--------------------------------------------------------------
        'DIM temp(layers, columns * rows) AS GXMapTile
        '--------------------------------------------------------------
        ' END temporary code - remove
        '--------------------------------------------------------------


        ' Read the map tile layer data
        Dim lstr As String
        lstr = __GX_ReadString
        lstr = _Inflate$(lstr)
        ' TODO: use a temp file strategy that will avoid file collisions
        '       or potentially read and write from memory instead
        If Not _DirExists("./tmp") Then MkDir "./tmp"
        Open "./tmp/layer" For Binary As #2
        Put #2, , lstr
        Close #2
        Open "./tmp/layer" For Binary As #2
        Get #2, , __gx_map_layers()
        Close #2
        Kill "./tmp/layer"

        '--------------------------------------------------------------
        ' temporary code - remove
        '--------------------------------------------------------------
        'DIM l AS INTEGER, t AS INTEGER
        'FOR l = 1 TO layers
        '    FOR t = 0 TO columns * rows
        '        gx_map_layers(t, l) = temp(l, t)
        '    NEXT t
        'NEXT l
        '--------------------------------------------------------------
        ' END temporary code - remove
        '--------------------------------------------------------------


        ' Read the tileset information
        __GX_TilesetLoad
        'DIM tilesetFilename AS STRING
        'DIM tilewidth AS INTEGER
        'DIM tileheight AS INTEGER
        'tilesetFilename = __GX_ReadString ' original tileset filename
        'GET #1, , tilewidth
        'GET #1, , tileheight

        '' If the map has an embedded tileset image, save it to a temp file location
        'DIM tsize AS LONG
        'GET #1, , tsize
        'IF NOT EOF(1) THEN
        '    DIM tmpfile AS STRING
        '    DIM bytes(tsize) AS _UNSIGNED _BYTE
        '    tmpfile = __GXFS_RemoveFileExtension(__GXFS_GetFilename(filename)) + ".ts1"
        '    IF NOT _DIREXISTS("./tmp") THEN MKDIR ("./tmp")
        '    tilesetFilename = "./tmp/" + tmpfile
        '    GET #1, , bytes()
        '    OPEN tilesetFilename FOR BINARY AS #2
        '    PUT #2, , bytes()
        '    CLOSE #2
        '    GXTilesetCreate tilesetFilename, tilewidth, tileheight
        'ELSE
        '    GXTilesetCreate tilesetFilename, tilewidth, tileheight
        'END IF

    End Sub


    ' Load a version 1 map
    Sub __GX_MapLoadV1 (filename As String)
        Dim columns As Integer
        Dim rows As Integer
        Dim col As Integer
        Dim row As Integer
        Dim layer As Integer
        Dim depth As Integer
        Dim tile As Integer

        Get #1, , columns
        Get #1, , rows

        GXMapCreate columns, rows, 3
        __gx_map.version = 1

        For row = 0 To rows - 1
            For col = 0 To columns - 1
                Get #1, , depth
                If depth > 0 Then
                    For layer = 1 To depth
                        Get #1, , tile
                        GXMapTile col, row, layer, tile + 1
                    Next layer
                End If
            Next col
        Next row

        ' Read the tileset information
        Dim tilesetFilename As String
        Dim tilewidth As Integer
        Dim tileheight As Integer
        tilesetFilename = __GX_ReadString_V1 ' original tileset filename
        Get #1, , tilewidth
        Get #1, , tileheight

        ' If the map has an embedded tileset image, save it to a temp file location
        Dim tsize As Long
        Get #1, , tsize
        If Not EOF(1) Then
            Dim tmpfile As String
            Dim bytes(tsize) As _Unsigned _Byte
            tmpfile = GXFS_RemoveFileExtension(GXFS_GetFilename(filename)) + ".ts1"
            If Not _DirExists("./tmp") Then MkDir ("./tmp")
            tilesetFilename = "./tmp/" + tmpfile
            Get #1, , bytes()
            Open tilesetFilename For Binary As #2
            Put #2, , bytes()
            Close #2
            GXTilesetCreate tilesetFilename, tilewidth, tileheight
        Else
            GXTilesetCreate tilesetFilename, tilewidth, tileheight
        End If
    End Sub



    Function GXMapIsometric
        GXMapIsometric = __gx_map.isometric
    End Function

    Sub GXMapIsometric (iso As Integer)
        __gx_map.isometric = iso
        __GX_UpdateSceneSize
    End Sub


    Sub GXSpriteDraw (i As Long, x As Long, y As Long, seq As Integer, frame As Integer, swidth As Integer, sheight As Integer, imgdest As Long)
        GXSpriteDrawScaled i, x, y, swidth, sheight, seq, frame, swidth, sheight, imgdest
    End Sub

    Sub GXSpriteDrawScaled (i As Long, x As Long, y As Long, dwidth As Integer, dheight As Integer, seq As Integer, frame As Integer, swidth As Integer, sheight As Integer, imgdest As Long)
        Dim xoffset, yoffset
        xoffset = (frame - 1) * swidth
        yoffset = (seq - 1) * sheight
        If imgdest Then
            ' i, imgdest
            'Dim a: Input a
            _PutImage (x, y)-(x + (dwidth - 1), y + (dheight - 1)), i, imgdest, (xoffset, yoffset)-(xoffset + (swidth - 1), yoffset + (sheight - 1))
        Else
            _PutImage (x, y)-(x + (dwidth - 1), y + (dheight - 1)), i, , (xoffset, yoffset)-(xoffset + (swidth - 1), yoffset + (sheight - 1))
        End If
    End Sub


    ' Tileset Methods
    ' ----------------------------------------------------------------------------
    Sub GXTilesetCreate (tilesetFilename As String, tileWidth As Integer, tileHeight As Integer)
        GXTilesetReplaceImage tilesetFilename, tileWidth, tileHeight

        ReDim __gx_tileset_tiles(GXTilesetColumns * GXTilesetRows) As GXTile
        'REDIM __gx_tileset_animations(0) AS GXTile
    End Sub

    Sub GXTilesetReplaceImage (tilesetFilename As String, tilewidth As Integer, tileheight As Integer)
        __gx_tileset.filename = tilesetFilename
        __gx_tileset.width = tilewidth
        __gx_tileset.height = tileheight

        Dim tswidth, tsheight
        __gx_tileset.image = __GX_ImageLoad(tilesetFilename)
        tswidth = _Width(GXTilesetImage)
        tsheight = _Height(GXTilesetImage)

        __gx_tileset.columns = tswidth / GXTilesetWidth
        __gx_tileset.rows = tsheight / GXTilesetHeight

        'gx_scene.columns = FIX(gx_scene.width / gx_tileset.width)
        'gx_scene.rows = FIX(gx_scene.height / gx_tileset.height)
        __GX_UpdateSceneSize
    End Sub

    Sub GXTilesetLoad (filename As String)
        Open filename For Binary As #1
        __GX_TilesetLoad
        Close #1
    End Sub

    Sub __GX_TilesetLoad
        Dim version As Integer
        version = 1

        ' Save the tileset version
        Get #1, , version

        ' Save the tileset image meta-data
        __gx_tileset.filename = __GX_ReadString
        Get #1, , __gx_tileset.width
        Get #1, , __gx_tileset.height

        ' Read the tileset image data and save to a temporary location
        Dim tsize As Long
        Get #1, , tsize
        Dim tmpfile As String
        Dim tilesetFilename As String
        Dim bytes(tsize) As _Unsigned _Byte
        tmpfile = GXFS_RemoveFileExtension(GXFS_GetFilename(GXTilesetFilename)) + ".gxi"
        If Not _DirExists("./tmp") Then MkDir ("./tmp")
        tilesetFilename = "./tmp/" + tmpfile
        Get #1, , bytes()
        Open tilesetFilename For Binary As #2
        Put #2, , bytes()
        Close #2
        GXTilesetCreate tilesetFilename, GXTilesetWidth, GXTilesetHeight

        ' Read the tileset tile data
        Dim asize As Integer
        Get #1, , asize
        ReDim __gx_tileset_tiles(asize) As GXTile
        Get #1, , __gx_tileset_tiles()

        ' Read the tileset animation data
        Get #1, , asize
        ReDim __gx_tileset_animations(asize) As GXTileFrame
        Get #1, , __gx_tileset_animations()
    End Sub

    Sub GXTilesetSave (filename As String)
        Open filename For Binary As #1
        __GX_TilesetSave
        Close #1
    End Sub

    Sub __GX_TilesetSave
        Dim version As Integer
        version = 1

        ' Save the tileset version
        Put #1, , version

        ' Save the tileset image meta-data
        __GX_WriteString GXTilesetFilename
        Put #1, , __gx_tileset.width
        Put #1, , __gx_tileset.height

        ' Save the tileset image data
        Dim tsize As Long
        Open GXTilesetFilename For Binary As #2
        tsize = LOF(2)
        Put #1, , tsize

        Dim bytes(tsize) As _Unsigned _Byte
        Get #2, , bytes()
        Put #1, , bytes()
        Close #2

        ' Save the tileset tile data
        Dim asize As Integer
        asize = UBound(__gx_tileset_tiles)
        Put #1, , asize
        Put #1, , __gx_tileset_tiles()

        ' Save the tileset animation data
        asize = UBound(__gx_tileset_animations)
        Put #1, , asize
        Put #1, , __gx_tileset_animations()
    End Sub

    Sub __GX_TilesetSaveWeb (tilesetFilename As String)

        Print #1, __JSL("tileset"); "{"

        ' Save the tileset version
        'Put #1, , version

        ' Save the tileset image meta-data
        Print #1, __JSL("width"); __gx_tileset.width; ","
        Print #1, __JSL("height"); __gx_tileset.height; ","
        Print #1, __JSL("image"); Chr$(34) + tilesetFilename + Chr$(34)

        ' Save the tileset image data
        Dim tsize As Long
        Open GXTilesetFilename For Binary As #2
        tsize = LOF(2)
        'Put #1, , tsize

        Open tilesetFilename For Binary As #3
        Dim bytes(tsize) As _Unsigned _Byte
        Get #2, , bytes()
        Put #3, , bytes()
        Close #2
        Close #3

        Print #1, "}"

        ' Save the tileset tile data
        'Dim asize As Integer
        'asize = UBound(__gx_tileset_tiles)
        'Put #1, , asize
        'Put #1, , __gx_tileset_tiles()

        ' Save the tileset animation data
        'asize = UBound(__gx_tileset_animations)
        'Put #1, , asize
        'Put #1, , __gx_tileset_animations()
    End Sub


    Sub GXTilesetPos (tilenum As Integer, p As GXPosition)
        If GXTilesetColumns = 0 Then
            p.x = 0
            p.y = 0
        Else
            p.y = Fix((tilenum - 1) / GXTilesetColumns)
            p.y = p.y + 1
            p.x = (tilenum - 1) Mod GXTilesetColumns + 1
        End If
    End Sub

    Function GXTilesetWidth
        GXTilesetWidth = __gx_tileset.width
    End Function

    Function GXTilesetHeight
        GXTilesetHeight = __gx_tileset.height
    End Function

    Function GXTilesetColumns
        GXTilesetColumns = __gx_tileset.columns
    End Function

    Function GXTilesetRows
        GXTilesetRows = __gx_tileset.rows
    End Function

    Function GXTilesetFilename$
        GXTilesetFilename = __gx_tileset.filename
    End Function

    Function GXTilesetImage
        GXTilesetImage = __gx_tileset.image
    End Function

    Sub GXTilesetAnimationCreate (tileId As Integer, animationSpeed As Integer)
        Dim frameId As Integer
        frameId = UBound(__gx_tileset_animations) + 1
        ReDim _Preserve __gx_tileset_animations(frameId) As GXTileFrame
        __gx_tileset_animations(frameId).tileId = tileId
        __gx_tileset_animations(frameId).firstFrame = frameId
        __gx_tileset_tiles(tileId).animationId = frameId
        __gx_tileset_tiles(tileId).animationSpeed = animationSpeed
    End Sub

    Sub GXTilesetAnimationAdd (firstTileId As Integer, addTileId As Integer)
        Dim firstFrame As Integer
        firstFrame = __gx_tileset_tiles(firstTileId).animationId

        ' find the last frame
        Dim lastFrame As Integer
        lastFrame = firstFrame
        While __gx_tileset_animations(lastFrame).nextFrame > 0
            lastFrame = __gx_tileset_animations(lastFrame).nextFrame
        Wend

        Dim frameId As Integer
        frameId = UBound(__gx_tileset_animations) + 1
        ReDim _Preserve __gx_tileset_animations(frameId) As GXTileFrame
        __gx_tileset_animations(frameId).tileId = addTileId
        __gx_tileset_animations(frameId).firstFrame = firstFrame
        __gx_tileset_animations(lastFrame).nextFrame = frameId
    End Sub

    Sub GXTilesetAnimationRemove (firstTileId As Integer)
        ' TODO: replace with implementation that will remove unused
        '       animation data from the array
        __gx_tileset_tiles(firstTileId).animationId = 0
    End Sub

    Function GXTilesetAnimationFrames (tileId As Integer, tileFrames() As Integer)
        If tileId < 0 Or tileId > GXTilesetRows * GXTilesetColumns Then
            GXTilesetAnimationFrames = 0
            Exit Function
        End If

        ReDim tileFrames(0) As Integer
        Dim frame As Integer
        Dim frameCount As Integer
        frameCount = 0
        frame = __gx_tileset_tiles(tileId).animationId
        While frame > 0
            frameCount = frameCount + 1
            ReDim _Preserve tileFrames(frameCount) As Integer
            tileFrames(frameCount) = __gx_tileset_animations(frame).tileId
            frame = __gx_tileset_animations(frame).nextFrame
        Wend
        GXTilesetAnimationFrames = frameCount
    End Function

    Function GXTilesetAnimationSpeed (tileId As Integer)
        If tileId > GXTilesetRows * GXTilesetColumns Then Exit Function
        GXTilesetAnimationSpeed = __gx_tileset_tiles(tileId).animationSpeed
    End Function

    Sub GXTilesetAnimationSpeed (tileId As Integer, speed As Integer)
        'IF tileId < 1 THEN EXIT SUB

        'DIM firstFrame AS INTEGER
        'firstFrame = gx_tileset_tiles(tileId).animationId
        'IF firstFrame < 1 THEN EXIT SUB

        'gx_tileset_animations(firstFrame).animationSpeed = speed
        __gx_tileset_tiles(tileId).animationSpeed = speed
    End Sub

    ' Bitmap Font Methods
    ' ----------------------------------------------------------------------------
    Function GXFontCreate (filename As String, charWidth As Integer, charHeight As Integer, charref As String)
        __gx_font_count = __gx_font_count + 1
        ReDim _Preserve __gx_fonts(__gx_font_count) As GXFont
        ReDim _Preserve __gx_font_charmap(256, __gx_font_count) As GXPosition

        ' Create a new game entity
        __gx_fonts(__gx_font_count).eid = GXEntityCreate(filename, charWidth, charHeight, 1)
        ' Hide the entity as we will not be displaying it as a normal sprite
        GXEntityHide __gx_fonts(__gx_font_count).eid

        ' map the character codes to the image location
        __GX_FontMapChars __gx_font_count, charref

        GXFontCreate = __gx_font_count
    End Function

    Sub GXFontCreate (filename As String, charWidth As Integer, charHeight As Integer, charref As String, uid As String)
        Dim fid As Integer
        fid = GXFontCreate(filename, charWidth, charHeight, charref)
        __GX uid, fid, GXTYPE_FONT
    End Sub

    Function GXFontWidth (fid As Long)
        GXFontWidth = GXEntityWidth(__gx_fonts(fid).eid)
    End Function

    Function GXFontHeight (fid As Long)
        GXFontHeight = GXEntityHeight(__gx_fonts(fid).eid)
    End Function

    Function GXFontCharSpacing% (fid As Long)
        GXFontCharSpacing = __gx_fonts(fid).charSpacing
    End Function

    Sub GXFontCharSpacing (fid As Long, charSpacing As Integer)
        __gx_fonts(fid).charSpacing = charSpacing
    End Sub

    Function GXFontLineSpacing% (fid As Long)
        GXFontLineSpacing = __gx_fonts(fid).lineSpacing
    End Function

    Sub GXFontLineSpacing (fid As Long, lineSpacing As Integer)
        __gx_fonts(fid).lineSpacing = lineSpacing
    End Sub

    Sub GXDrawText (fid As Integer, sx As Long, sy As Long, s As String)
        If fid = GXFONT_DEFAULT Or fid = GXFONT_DEFAULT_BLACK Then
            If __gx_fonts(fid).eid = 0 Then __GX_FontCreateDefault fid
        End If

        Dim i As Integer
        Dim a As Integer
        Dim c As String
        Dim cpos As GXPosition
        Dim x As Long: x = sx
        Dim y As Long: y = sy
        Dim font As GXFont: font = __gx_fonts(fid)
        Dim e As GXEntity: e = __gx_entities(font.eid)

        For i = 1 To Len(s)
            c = Mid$(s, i, 1)
            a = Asc(c)
            If a = 10 Then ' Line feed, move down to the next line
                x = sx
                y = y + e.height + font.lineSpacing
            ElseIf Not a = 13 Then ' Ignore Carriage Return
                If Not a = 32 Then ' Space character, nothing to draw
                    cpos = __gx_font_charmap(a, fid)
                    GXSpriteDraw e.image, x, y, cpos.y, cpos.x, e.width, e.height, __gx_scene.image '0
                End If
                x = x + e.width + font.charSpacing
            End If
        Next i
    End Sub



    ' Debugging methods
    ' ----------------------------------------------------------------------------
    Function GXDebug
        GXDebug = __gx_debug.enabled
    End Function

    Sub GXDebug (enabled As Integer)
        __gx_debug.enabled = enabled
    End Sub

    Function GXDebugScreenEntities
        GXDebugScreenEntities = __gx_debug.screenEntities
    End Function

    Sub GXDebugScreenEntities (enabled As Integer)
        __gx_debug.screenEntities = enabled
    End Sub

    Function GXDebugFont%
        GXDebugFont = __gx_debug.font
    End Function

    Sub GXDebugFont (font As Integer)
        __gx_debug.font = font
    End Sub

    Function GXDebugTileBorderColor~&
        GXDebugTileBorderColor = __gx_debug.tileBorderColor
    End Function

    Sub GXDebugTileBorderColor (c As _Unsigned Long)
        __gx_debug.tileBorderColor = c
    End Sub

    Function GXDebugEntityBorderColor~&
        GXDebugEntityBorderColor = __gx_debug.entityBorderColor
    End Function

    Sub GXDebugEntityBorderColor (c As _Unsigned Long)
        __gx_debug.entityBorderColor = c
    End Sub

    Function GXDebugEntityCollisionColor~&
        GXDebugEntityCollisionColor = __gx_debug.entityCollisionColor
    End Function

    Sub GXDebugEntityCollisionColor (c As _Unsigned Long)
        __gx_debug.entityCollisionColor = c
    End Sub

    Sub __GX_DebugMapTile

        Dim t As Integer, tx As Long, ty As Long, depth As Integer, i As Integer
        Dim tpos As GXPosition
        '__GX_MapTilePosAt _MOUSEX, _MOUSEY, tpos
        GXMapTilePosAt GXMouseX, GXMouseY, tpos
        depth = GXMapTileDepth(tpos.x, tpos.y)
        tx = tpos.x * GXTilesetWidth - GXSceneX
        ty = tpos.y * GXTilesetHeight - GXSceneY

        If Not GXHardwareAcceleration Then
            Dim cdest As Long
            cdest = _Dest
            _Dest __gx_scene.image

            Line (tx, ty)-(tx + GXTilesetWidth - 1, ty + GXTilesetHeight - 1), GXDebugTileBorderColor, B , &B1010101010101010

            _Dest cdest
        End If
        GXDrawText GXDebugFont, tx, ty - 8, "(" + _Trim$(Str$(tpos.x)) + "," + _Trim$(Str$(tpos.y)) + ")"
        For i = 1 To depth
            t = GXMapTile(tpos.x, tpos.y, i)
            GXDrawText GXDebugFont, tx, ty + GXTilesetHeight + 1 + (i - 1) * 8, _Trim$(Str$(i)) + ":" + _Trim$(Str$(t))
        Next i

    End Sub

    Sub __GX_DebugEntity (ent As GXEntity, x, y)
        If ent.screen And Not GXDebugScreenEntities Then Exit Sub

        ' Display the entity's position
        GXDrawText GXDebugFont, x, y - 8, "(" + __GX_DebugRound(ent.x, 2) + "," + __GX_DebugRound(ent.y, 2) + ")"

        If Not GXHardwareAcceleration Then
            Dim odest As Long
            odest = _Dest
            _Dest __gx_scene.image

            ' Draw the entity's collision rect
            Line (x + ent.coLeft, y + ent.coTop)-(x + ent.width - 1 - ent.coRight, y + ent.height - 1 - ent.coBottom), GXDebugEntityCollisionColor, B ', &B1010101010101010

            ' Draw the entity's bounding rect
            Line (x, y)-(x + ent.width - 1, y + ent.height - 1), GXDebugEntityBorderColor, B , &B1010101010101010

            _Dest odest
        End If
    End Sub

    Function __GX_DebugRound$ (n As Double, decimalPlaces As Integer)
        Dim n2 As Long
        n2 = _Round(n * 10 ^ decimalPlaces)
        If n2 = 0 Then
            __GX_DebugRound = "0." + String$(decimalPlaces, "0")
        Else
            Dim ns As String, decimal As String
            ns = _Trim$(Str$(n2))
            decimal = Right$(ns, decimalPlaces)
            ns = Left$(ns, Len(ns) - decimalPlaces)
            __GX_DebugRound = ns + "." + decimal
        End If
    End Function

    Sub __GX_DebugFrameRate
        Dim frame As String
        Dim frameRate As String
        frame = _Trim$(Str$(GXFrame))
        frameRate = _Trim$(Str$(GXFrameRate))
        frameRate = String$(Len(frame) - Len(frameRate), " ") + frameRate

        'Dim odest As Long
        'odest = _Dest
        '_Dest __gx_scene.image
        GXDrawText GXDebugFont, GXSceneWidth - (Len(frame) + 6) * 6 - 1, 1, "FRAME:" + frame
        GXDrawText GXDebugFont, GXSceneWidth - (Len(frameRate) + 4) * 6 - 1, 9, "FPS:" + frameRate
        '_Dest odest
    End Sub



    ' GX Engine Miscellaneous Private Methods
    ' ----------------------------------------------------------------------------
    Function __GX_ImageLoad& (filename As String)
        Dim id As Long
        Dim i As Integer
        For i = 0 To __gx_image_count - 1
            If filename = __gx_images(i).filename Then id = __gx_images(i).id
        Next i
        If id = 0 Then
            'If Not GXSceneEmbedded Then
            If GXHardwareAcceleration Then
                id = _LoadImage(filename, 33)
            Else
                id = _LoadImage(filename, 32) ', 33)
            End If
            ' "load image id: ", id
            'Dim a: Input a
            __gx_image_count = __gx_image_count + 1
            ReDim _Preserve __gx_images(__gx_image_count) As GXImage
            __gx_images(__gx_image_count - 1).id = id
            __gx_images(__gx_image_count - 1).filename = filename
        End If
        ' "returning id: ", id
        'Input a
        __GX_ImageLoad = id
    End Function


    Sub __GX_UpdateSceneSize
        If GXTilesetWidth < 1 Or GXTilesetHeight < 1 Then Exit Sub
        If GXMapIsometric Then
            __gx_scene.columns = Fix(GXSceneWidth / GXTilesetWidth)
            'gx_scene.rows = FIX(GXSceneHeight / (GXTilesetHeight * .25))
            __gx_scene.rows = GXSceneHeight / (GXTilesetWidth / 4)
        Else
            __gx_scene.columns = Fix(GXSceneWidth / GXTilesetWidth)
            __gx_scene.rows = Fix(GXSceneHeight / GXTilesetHeight)
        End If
    End Sub

    Sub __GX_WriteString (s As String)
        Dim l As Long
        l = Len(s)
        Put #1, , l
        Put #1, , s
    End Sub

    Function __GX_ReadString$
        Dim s As String
        Dim l As Long
        Dim i As Integer
        Get #1, , l
        s = String$(l, " ")
        Get #1, , s
        __GX_ReadString = s
    End Function

    Function __GX_ReadString_V1$
        Dim s As String
        Dim l As Integer
        Dim i As Integer
        Dim b As _Unsigned _Byte
        Get #1, , l
        For i = 1 To l
            Get #1, , b
            s = s + Chr$(b)
        Next i
        __GX_ReadString_V1 = s
    End Function

    Function __GX_EntityCollide (eid1, eid2)
        __GX_EntityCollide = __GX_RectCollide(GXEntityX(eid1), GXEntityY(eid1), GXEntityWidth(eid1), GXEntityHeight(eid1), GXEntityX(eid2), GXEntityY(eid2), GXEntityWidth(eid2), GXEntityHeight(eid2))
    End Function

    Function __GX_RectCollide (r1x1%, r1y1%, r1w%, r1h%, r2x1%, r2y1%, r2w%, r2h%)

        Dim r1x2%, r1y2%, r2x2%, r2y2%
        r1x2% = r1x1% + r1w%
        r1y2% = r1y1% + r1h%
        r2x2% = r2x1% + r2w%
        r2y2% = r2y1% + r2h%

        __GX_RectCollide = 0
        If r1x2% >= r2x1% Then
            If r1x1% <= r2x2% Then
                If r1y2% >= r2y1% Then
                    If r1y1% <= r2y2% Then
                        __GX_RectCollide = 1
                    End If
                End If
            End If
        End If

    End Function

    Sub __GX_FontMapChars (fid As Long, charref As String)
        Dim cx As Integer, cy As Integer, i As Integer, a As Integer
        cx = 1
        cy = 1
        For i = 1 To Len(charref)
            a = Asc(Mid$(charref, i, 1))
            If a = 10 Then
                cx = 1
                cy = cy + 1
            Else
                If a >= 33 And a <= 256 Then
                    __gx_font_charmap(a, fid).x = cx
                    __gx_font_charmap(a, fid).y = cy
                End If
                cx = cx + 1
            End If
        Next i
    End Sub

    Sub __GX_FontCreateDefault (fid As Long)
        Dim filename As String
        If fid = GXFONT_DEFAULT_BLACK Then
            __gx_font_default_black
            filename = "tmp/__gx_font_default_black.png"
        Else
            __gx_font_default
            filename = "tmp/__gx_font_default.png"
        End If

        __gx_fonts(fid).eid = GXEntityCreate(filename, 6, 8, 1)
        Kill filename
        GXEntityHide __gx_fonts(fid).eid
        __GX_FontMapChars fid, "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789~`!@#$%^&*()_-+={}[]|\,./<>?:;" + Chr$(34) + "'"
        GXFontLineSpacing fid, 1
    End Sub


    Function __GX_EntityTestMove (entity As Integer, mx As Long, my As Long, tpos As GXPosition, collisionEntity As Integer)
        tpos.x = -1
        tpos.y = -1
        'IF nocollision THEN GX_EntityTestMove = 1: EXIT FUNCTION

        Dim tcount As Integer
        ReDim tiles(0) As GXPosition
        __GX_EntityCollisionTiles entity, mx, my, tiles(), tcount


        Dim move As Integer
        move = 1

        ' Test for tile collision
        Dim i As Integer, j As Integer
        Dim tile As Integer
        For i = 0 To tcount - 1
            'FOR j = 1 TO GXMapTileDepth(tiles(i).x, tiles(i).y)
            'tile = GXMapTile(tiles(i).x, tiles(i).y, j)
            Dim e As GXEvent
            e.entity = entity
            e.event = GXEVENT_COLLISION_TILE
            e.collisionTileX = tiles(i).x
            e.collisionTileY = tiles(i).y
            GXOnGameEvent e
            If e.collisionResult Then
                move = 0
                tpos = tiles(i)
            End If
            'NEXT j
        Next i

        ' Test for entity collision
        Dim entities(0) As Integer
        Dim ecount As Integer
        ecount = __GX_EntityCollision(entity, mx, my, entities())
        For i = 1 To ecount
            Dim ee As GXEvent
            ee.entity = entity
            ee.event = GXEVENT_COLLISION_ENTITY
            ee.collisionEntity = entities(i)
            GXOnGameEvent ee
            If ee.collisionResult Then
                move = 0
                collisionEntity = entities(i)
            End If
        Next i

        __GX_EntityTestMove = move
    End Function

    Function __GX_EntityCollision (eid As Integer, movex As Integer, movey As Integer, entities() As Integer) ', ecount AS INTEGER)
        Dim ecount As Integer
        ecount = 0

        Dim i As Integer
        For i = 1 To __gx_entity_count
            If i <> eid Then
                ' TODO: only include entities that should be considered (e.g. visible, non-screen-level)
                IF __GX_RectCollide(GXEntityX(eid) + GXEntityCollisionOffsetLeft(eid) + movex, _
                   GXEntityY(eid) + GXEntityCollisionOffsetTop(eid) + movey, _
                   GXEntityWidth(eid) - GXEntityCollisionOffsetLeft(eid) - GXEntityCollisionOffsetRight(eid) - 1, _
                   GXEntityHeight(eid) - GXEntityCollisionOffsetTop(eid) - GXEntityCollisionOffsetBottom(eid) - 1, _
                   GXEntityX(i) + GXEntityCollisionOffsetLeft(i), _
                   GXEntityY(i) + GXEntityCollisionOffsetTop(i), _
                   GXEntityWidth(i) - GXEntityCollisionOffsetLeft(i) - GXEntityCollisionOffsetRight(i) - 1, _
                   GXEntityHeight(i) - GXEntityCollisionOffsetTop(i) - GXEntityCollisionOffsetBottom(i)-1) THEN
                    ecount = ecount + 1
                    ReDim _Preserve entities(ecount) As Integer
                    entities(ecount) = i
                End If
            End If
        Next i

        __GX_EntityCollision = ecount
    End Function

    Sub __GX_EntityCollisionTiles (entity As Integer, movex As Integer, movey As Integer, tiles() As GXPosition, tcount As Integer)
        Dim tx As Integer, ty As Integer
        Dim tx0 As Integer, txn As Integer
        Dim ty0 As Integer, tyn As Integer
        Dim x As Integer, y As Integer, i As Integer

        If movex <> 0 Then
            Dim startx As Integer
            startx = -1 + GXEntityCollisionOffsetLeft(entity) 'cx
            If movex > 0 Then startx = GXEntityWidth(entity) + movex - GXEntityCollisionOffsetRight(entity) 'cx2
            tx = Fix((GXEntityX(entity) + startx) / GXTilesetWidth)

            ' This is a real brute force way to find the intersecting tiles.
            ' We're basically testing every pixel along the edge of the entity's
            ' collision rect and incrementing the collision tile count.
            ' With a bit more math I'm sure we could avoid some extra loops here.
            tcount = 0
            ty0 = 0
            For y = GXEntityY(entity) + GXEntityCollisionOffsetTop(entity) To GXEntityY(entity) + GXEntityHeight(entity) - 1 - GXEntityCollisionOffsetBottom(entity)
                ty = Fix(y / GXTilesetHeight)
                If tcount = 0 Then ty0 = ty
                If Not ty = tyn Then
                    tcount = tcount + 1
                End If
                tyn = ty
            Next y

            ' Add the range of detected tile positions to the return list
            ReDim tiles(tcount) As GXPosition
            i = 0
            For ty = ty0 To tyn
                tiles(i).x = tx
                tiles(i).y = ty
                i = i + 1
            Next ty
        End If

        If movey <> 0 Then
            Dim starty As Integer
            starty = -1 + GXEntityCollisionOffsetTop(entity) 'cy
            If movey > 0 Then starty = GXEntityHeight(entity) + movey - GXEntityCollisionOffsetBottom(entity) 'cy2
            ty = Fix((GXEntityY(entity) + starty) / GXTilesetHeight)

            ' This is a real brute force way to find the intersecting tiles.
            ' We're basically testing every pixel along the edge of the entity's
            ' collision rect and incrementing the collision tile count.
            ' With a bit more math I'm sure we could avoid some extra loops here.
            tcount = 0
            tx0 = 0
            For x = GXEntityX(entity) + GXEntityCollisionOffsetLeft(entity) To GXEntityX(entity) + GXEntityWidth(entity) - 1 - GXEntityCollisionOffsetRight(entity)
                tx = Fix(x / GXTilesetWidth)
                If tcount = 0 Then tx0 = tx
                If Not tx = txn Then
                    tcount = tcount + 1
                End If
                txn = tx
            Next x


            ReDim tiles(tcount) As GXPosition
            i = 0
            For tx = tx0 To txn
                tiles(i).x = tx
                tiles(i).y = ty
                i = i + 1
            Next tx
        End If
    End Sub

    Sub GXKeyInput (k As Long, di As GXDeviceInput)
        di.deviceId = 1
        di.deviceType = GXDEVICE_KEYBOARD
        di.inputType = GXDEVICE_BUTTON
        di.inputId = k
        di.inputValue = -1
    End Sub

    Function GXKeyDown (k As Long)
        Dim di As GXDeviceInput
        GXKeyInput k, di
        GXKeyDown = GXDeviceInputTest(di)
    End Function

    Function __GX_ActionTest (a As GXAction)
        If a.disabled Then
            __GX_ActionTest = GX_FALSE
        Else
            Dim di As GXDeviceInput
            __GX_ActionInput a, di
            __GX_ActionTest = GXDeviceInputTest(di)
        End If
    End Function

    Sub __GX_PlayerAction (pid As Integer)
        Dim tpos As GXPosition
        Dim centity As Integer

        Dim player As GXPlayer
        player = __gx_players(pid)

        Dim actionLeft As GXAction, actionRight As GXAction, actionUp As GXAction, actionDown As GXAction
        Dim actionJump As GXAction, actionJumpRight As GXAction, actionJumpLeft As GXAction
        actionLeft = __gx_player_keymap(GXACTION_MOVE_LEFT, pid)
        actionRight = __gx_player_keymap(GXACTION_MOVE_RIGHT, pid)
        actionUp = __gx_player_keymap(GXACTION_MOVE_UP, pid)
        actionDown = __gx_player_keymap(GXACTION_MOVE_DOWN, pid)
        actionJump = __gx_player_keymap(GXACTION_JUMP, pid)
        actionJumpRight = __gx_player_keymap(GXACTION_JUMP_RIGHT, pid)
        actionJumpLeft = __gx_player_keymap(GXACTION_JUMP_LEFT, pid)

        If __GX_ActionTest(actionDown) Then
            GXEntityAnimate player.eid, actionDown.animationSeq, actionDown.animationSpeed
            If __GX_EntityTestMove(player.eid, 0, 1, tpos, centity) Then
                __gx_entities(player.eid).vy = player.walkSpeed
            Else
                If Not __gx_entities(player.eid).applyGravity Then __gx_entities(player.eid).vy = 0
            End If
            __gx_entities(player.eid).vx = 0
            __GX_PlayerEvent pid, actionDown.type

        ElseIf __GX_ActionTest(actionUp) Then
            GXEntityAnimate player.eid, actionUp.animationSeq, actionUp.animationSpeed
            If __GX_EntityTestMove(player.eid, 0, -1, tpos, centity) Then
                __gx_entities(player.eid).vy = -player.walkSpeed
            Else
                If Not __gx_entities(player.eid).applyGravity Then __gx_entities(player.eid).vy = 0
            End If
            __gx_entities(player.eid).vx = 0
            __GX_PlayerEvent pid, actionUp.type

        ElseIf __GX_ActionTest(actionRight) Then
            GXEntityAnimate player.eid, actionRight.animationSeq, actionRight.animationSpeed
            If __GX_EntityTestMove(player.eid, 1, 0, tpos, centity) Then
                __gx_entities(player.eid).vx = player.walkSpeed
            Else
                __gx_entities(player.eid).vx = 0
            End If
            If __gx_entities(player.eid).applyGravity = 0 Then __gx_entities(player.eid).vy = 0
            __GX_PlayerEvent pid, actionRight.type

        ElseIf __GX_ActionTest(actionLeft) Then
            GXEntityAnimate player.eid, actionLeft.animationSeq, actionLeft.animationSpeed
            If __GX_EntityTestMove(player.eid, -1, 0, tpos, centity) Then
                __gx_entities(player.eid).vx = -player.walkSpeed
            Else
                __gx_entities(player.eid).vx = 0
            End If
            If __gx_entities(player.eid).applyGravity = 0 Then __gx_entities(player.eid).vy = 0
            __GX_PlayerEvent pid, actionLeft.type

        Else
            GXEntityAnimateStop player.eid
            __gx_entities(player.eid).vx = 0
            If __gx_entities(player.eid).applyGravity = 0 Then
                __gx_entities(player.eid).vy = 0
            End If
        End If

        If __gx_entities(player.eid).applyGravity = 1 Then
            If __GX_ActionTest(actionJump) Or __GX_ActionTest(actionJumpLeft) Or __GX_ActionTest(actionJumpRight) Then
                If __GX_EntityTestMove(player.eid, 0, 1, tpos, centity) = 0 Then
                    __gx_entities(player.eid).vy = -player.jumpSpeed
                    __gx_entities(player.eid).jumping = 1
                    __GX_PlayerEvent pid, GXACTION_JUMP
                End If
            End If

            If __gx_entities(player.eid).jumping Then
                If __gx_entities(player.eid).vx < 0 And actionJumpLeft.animationSeq Then GXEntityAnimate player.eid, actionJumpLeft.animationSeq, actionJumpLeft.animationSpeed
                If __gx_entities(player.eid).vx > 0 And actionJumpRight.animationSeq Then GXEntityAnimate player.eid, actionJumpRight.animationSeq, actionJumpRight.animationSpeed
            End If
        End If

    End Sub

    Sub __GX_PlayerEvent (player As Integer, action As Integer)
        Dim e As GXEvent
        e.event = GXEVENT_PLAYER_ACTION
        e.player = player
        e.action = action
        GXOnGameEvent e
    End Sub


    Sub GXDeviceInputDetect (di As GXDeviceInput)
        Dim found As Integer
        Dim dcount As Integer
        dcount = _Devices

        While _DeviceInput
            ' Flush the input buffer
        Wend

        Do
            _Limit 90
            Dim x As Integer
            x = _DeviceInput
            If x Then
                Dim i As Integer
                For i = 1 To _LastButton(x)
                    If _Button(i) Then
                        di.deviceId = x
                        di.deviceType = __GX_DeviceTypeName(x)
                        di.inputType = GXDEVICE_BUTTON
                        di.inputId = i
                        di.inputValue = _Button(i)
                        found = 1
                        Exit Do
                    End If
                Next i

                For i = 1 To _LastAxis(x)
                    If _Axis(i) And Abs(_Axis(i)) = 1 Then
                        di.deviceId = x
                        di.deviceType = __GX_DeviceTypeName(x)
                        di.inputType = GXDEVICE_AXIS
                        di.inputId = i
                        di.inputValue = _Axis(i)
                        found = 1
                        Exit Do
                    End If
                Next i

                For i = 1 To _LastWheel(x)
                    If _Wheel(i) Then
                        di.deviceId = x
                        di.deviceType = __GX_DeviceTypeName(x)
                        di.inputType = GXDEVICE_WHEEL
                        di.inputId = i
                        di.inputValue = _Wheel(i)
                        found = 1
                        Exit Do
                    End If
                Next i
            End If
            ' No device input found, as a workaround let's loop through the key map looking for a keydown
            For i = 1 To UBound(__gx_keymap)
                Dim keyIsDown As Integer, inputId As Integer
                keyIsDown = GX_FALSE
                If __gx_keymap(i).value <> 0 Then
                    'If i > 29 Then
                    '    Print i; __gx_keymap(i).value
                    '    Dim x: Input x
                    'End If
                    If _KeyDown(__gx_keymap(i).value) Then
                        keyIsDown = GX_TRUE
                        inputId = i
                    ElseIf __gx_keymap(i).shift <> 0 Then
                        If _KeyDown(__gx_keymap(i).shift) Then
                            keyIsDown = GX_TRUE
                            inputId = i
                        End If
                    End If
                End If
                If keyIsDown Then
                    di.deviceId = GXDEVICE_KEYBOARD
                    di.deviceType = __GX_DeviceTypeName(GXDEVICE_KEYBOARD)
                    di.inputType = GXDEVICE_BUTTON
                    di.inputId = inputId
                    di.inputValue = GX_TRUE
                    found = 1
                    Exit Do
                End If
            Next i
        Loop Until found

        While _DeviceInput
            '    Flush the device input buffer
        Wend
        _KeyClear

    End Sub

    Function __GX_DeviceTypeName (deviceId)
        Dim dname As String
        dname = _Device$(deviceId)

        If InStr(dname, "[KEYBOARD]") Then
            __GX_DeviceTypeName = GXDEVICE_KEYBOARD
        ElseIf InStr(dname, "[MOUSE]") Then
            __GX_DeviceTypeName = GXDEVICE_MOUSE
        ElseIf InStr(dname, "[CONTROLLER]") Then
            __GX_DeviceTypeName = GXDEVICE_CONTROLLER
        End If
    End Function

    Function GXDeviceName$ (deviceId As Integer)
        Dim nstart As Integer, nend As Integer
        Dim dname As String
        dname = _Device$(deviceId)
        If InStr(dname, "[CONTROLLER]") Then
            nstart = InStr(dname, "[NAME]")
            If nstart = 0 Then
                dname = "Controller"
            Else
                nstart = nstart + 7
                nend = InStr(nstart, dname, "]]")
                dname = _Trim$(Mid$(dname, nstart, nend - nstart))
            End If
        ElseIf InStr(dname, "[MOUSE]") Then
            dname = "Mouse"
        ElseIf InStr(dname, "[KEYBOARD]") Then
            dname = "Keyboard"
        End If
        GXDeviceName = dname
    End Function

    Function GXDeviceTypeName$ (dtype As Integer)
        Dim dtypename As String
        Select Case dtype
            Case GXDEVICE_KEYBOARD: dtypename = "KEYBOARD"
            Case GXDEVICE_MOUSE: dtypename = "MOUSE"
            Case GXDEVICE_CONTROLLER: dtypename = "CONTROLLER"
        End Select
        GXDeviceTypeName = dtypename
    End Function

    Function GXInputTypeName$ (itype As Integer)
        Dim itypename As String
        Select Case itype
            Case GXDEVICE_BUTTON: itypename = "BUTTON"
            Case GXDEVICE_AXIS: itypename = "AXIS"
            Case GXDEVICE_WHEEL: itypename = "WHEEL"
        End Select
        GXInputTypeName = itypename
    End Function

    Function GXKeyButtonName$ (inputId As Integer)
        Dim k As String
        Select Case inputId
            Case GXKEY_ESC: k = "Esc"
            Case GXKEY_1: k = "1"
            Case GXKEY_2: k = "2"
            Case GXKEY_3: k = "3"
            Case GXKEY_4: k = "4"
            Case GXKEY_5: k = "5"
            Case GXKEY_6: k = "6"
            Case GXKEY_7: k = "7"
            Case GXKEY_8: k = "8"
            Case GXKEY_9: k = "9"
            Case GXKEY_0: k = "0"
            Case GXKEY_DASH: k = "-"
            Case GXKEY_EQUALS: k = "="
            Case GXKEY_BACKSPACE: k = "Bksp"
            Case GXKEY_TAB: k = "Tab"
            Case GXKEY_Q: k = "Q"
            Case GXKEY_W: k = "W"
            Case GXKEY_E: k = "E"
            Case GXKEY_R: k = "R"
            Case GXKEY_T: k = "T"
            Case GXKEY_Y: k = "Y"
            Case GXKEY_U: k = "U"
            Case GXKEY_I: k = "I"
            Case GXKEY_O: k = "O"
            Case GXKEY_P: k = "P"
            Case GXKEY_LBRACKET: k = "["
            Case GXKEY_RBRACKET: k = "]"
            Case GXKEY_ENTER: k = "Enter"
            Case GXKEY_LCTRL: k = "LCtrl"
            Case GXKEY_A: k = "A"
            Case GXKEY_S: k = "S"
            Case GXKEY_D: k = "D"
            Case GXKEY_F: k = "F"
            Case GXKEY_G: k = "G"
            Case GXKEY_H: k = "H"
            Case GXKEY_J: k = "J"
            Case GXKEY_K: k = "K"
            Case GXKEY_L: k = "L"
            Case GXKEY_SEMICOLON: k = ";"
            Case GXKEY_QUOTE: k = "'"
            Case GXKEY_BACKQUOTE: k = "`"
            Case GXKEY_LSHIFT: k = "LShift"
            Case GXKEY_BACKSLASH: k = "\"
            Case GXKEY_Z: k = "Z"
            Case GXKEY_X: k = "X"
            Case GXKEY_C: k = "C"
            Case GXKEY_V: k = "V"
            Case GXKEY_B: k = "B"
            Case GXKEY_N: k = "N"
            Case GXKEY_M: k = "M"
            Case GXKEY_COMMA: k = ","
            Case GXKEY_PERIOD: k = "."
            Case GXKEY_SLASH: k = "/"
            Case GXKEY_RSHIFT: k = "RShift"
            Case GXKEY_NUMPAD_MULTIPLY: k = "NPad *"
            Case GXKEY_SPACEBAR: k = "Space"
            Case GXKEY_CAPSLOCK: k = "CapsLk"
            Case GXKEY_F1: k = "F1"
            Case GXKEY_F2: k = "F2"
            Case GXKEY_F3: k = "F3"
            Case GXKEY_F4: k = "F4"
            Case GXKEY_F5: k = "F5"
            Case GXKEY_F6: k = "F6"
            Case GXKEY_F7: k = "F7"
            Case GXKEY_F8: k = "F8"
            Case GXKEY_F9: k = "F9"
            Case GXKEY_PAUSE: k = "Pause"
            Case GXKEY_SCRLK: k = "ScrLk"
            Case GXKEY_NUMPAD_7: k = "NPad 7"
            Case GXKEY_NUMPAD_8: k = "NPad 8"
            Case GXKEY_NUMPAD_9: k = "NPad 9"
            Case GXKEY_NUMPAD_MINUS: k = "-"
            Case GXKEY_NUMPAD_4: k = "NPad 4"
            Case GXKEY_NUMPAD_5: k = "NPad 5"
            Case GXKEY_NUMPAD_6: k = "NPad 6"
            Case GXKEY_NUMPAD_PLUS: k = "+"
            Case GXKEY_NUMPAD_1: k = "NPad 1"
            Case GXKEY_NUMPAD_2: k = "NPad 2"
            Case GXKEY_NUMPAD_3: k = "NPad 3"
            Case GXKEY_NUMPAD_0: k = "NPad 0"
            Case GXKEY_NUMPAD_PERIOD: k = "NPad ."
            Case GXKEY_F11: k = "F11"
            Case GXKEY_F12: k = "F12"
            Case GXKEY_NUMPAD_ENTER: k = "NPad Enter"
            Case GXKEY_RCTRL: k = "RCtrl"
            Case GXKEY_NUMPAD_DIVIDE: k = "NPad /"
            Case GXKEY_NUMLOCK: k = "NumLk"
            Case GXKEY_HOME: k = "Home"
            Case GXKEY_UP: k = "Up"
            Case GXKEY_PAGEUP: k = "PgUp"
            Case GXKEY_LEFT: k = "Left"
            Case GXKEY_RIGHT: k = "Right"
            Case GXKEY_END: k = "End"
            Case GXKEY_DOWN: k = "Down"
            Case GXKEY_PAGEDOWN: k = "PgDn"
            Case GXKEY_INSERT: k = "Ins"
            Case GXKEY_DELETE: k = "Del"
            Case GXKEY_LWIN: k = "LWin"
            Case GXKEY_RWIN: k = "RWin"
            Case GXKEY_MENU: k = "Menu"
        End Select
        GXKeyButtonName = k
    End Function

    '$Include: 'gx_fs.bm'
    '$Include: 'resource/font-default.png.bm'
    '$Include: 'resource/font-default-black.png.bm'

    $Let GXBM = 1
$End If
